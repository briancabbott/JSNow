/*

// Generator configuration language 
// Files extension should be .genconf

// Perform for each object of particular type.
// Target file name is constructed by evalutation of Python expression.
// The expression may reference variables defined in "for" clause and
// all generator parameters.

Entity:
    for Entity as entity
        "angular/form.html" => "forms/%s.html" % entity.name

// collect all object of particular type and perform once for the list.
Enums:
    for all Enums as enums
        "flask/enums.py.template" => "flask/enums.py"

Model:
    for Entity, Enum as entities, enums
        "flask/models.py.template" => "flask/models.py"

*/

ConfigurationFile:

    ('generator' gen_name=ID)?      // If generator name is not given it is
                                    // deduced from the file name.
    params*=Param                  // Optional generator parameters

    ('models' '{'
        models*=FilePath     // Relative file path in the "models"
    '}')?

    ('output' output=FilePath)?    // Output folder where code should be
                                   // generated. Default is 'src'

    rules*=GenRule
;

Param:
    name=ID '=' value=BASETYPE
;

GenRule:
    name=ID ':'
    'for' all?='all' types+=ID[','] 'as' var_names+=ID[',']
        trans+=TemplateTransformation
;

TemplateTransformation:
    template_path=FilePath '=>' python_path_expr=/[^\n]*/
    // python_path_expr is a python expression that should result in a
    // file path name of the target path. Everything until the end of line
    // will be passed to Python eval. The context of eval execution will
    // contain collected objects and generator parameters.
;

FilePath:
    STRING
;

Comment:
  /\/\/.*$/|/\/\*(.|\n)*?\*\//
;
