tximport StateMachine from "./../../tx_samples/state_machine/state_machine.tx"

style:
    // contained nodes when compound nodes selected
    {"selector": "node:selected node"
    , "style": {
            "background-color": "red", "border-color": "red", "color": "red"
        }
    }
:style end

view StateMachine as None

view State as Roundrectangle
{
    label: State.name
    {
        font: black 21 italic bold
        background: white 0.2
        outline: white 2
        margin: 0 25
    }
    link
    {
        // from: this // implicit if not defined
        to: State.transitions.to_state
        {
            arrow: black 2 triangle filled
            distance: 5
        }
        label: State.transitions.event.name
        {
            font: 18
        }
        line: black 2
        selected as curved [50, -50]
        {
            to: State.transitions.to_state
            {
                arrow: red 2 triangle hollow
                distance: 1
            }
            line: red 4
            label
            {
                font: red 21 bold
            }
        }
    }
    width: label
    background: white 0.4
    border: black 4 solid
    padding: 35
    selected
    {
        background: red 0.1
        border: red 4
        label
        {
            font: red 21 italic bold
        }
    }
}

view Command as Vee child of State
{
    label: Command.name
    {
        font: 18
        background: white 1 round
    }
    background: black 0
    border: white 0 solid 0
}

view Event as Vee inside Rectangle where this = StateMachine.resetEvents
{
    label: Event.name
    {
        font: 18
        background: white 1 round
    }
    background: black 0
    border: white 0 solid 0
    container
    {
        label: 'Reset Events:'
        {
            font: black 21 italic bold
            background: white 0.2
            outline: white 2
            margin: 0 25
        }
        width: label
        background: white 0.4
        border: black 4 solid
        padding: 35 px
        selected
        {
            label
            {
                font: red 21 italic bold
            }
            background: red 0.1
            border: red 4
        }
    }
}