<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 3.2//EN">
<HTML>
 <HEAD>
   <TITLE> [antlr-interest] Fortran grammars
   </TITLE>
   <LINK REL="Index" HREF="index.html" >
   <LINK REL="made" HREF="mailto:antlr-interest%40antlr.org?Subject=%5Bantlr-interest%5D%20Fortran%20grammars&In-Reply-To=">
   <META NAME="robots" CONTENT="index,nofollow">
   <META http-equiv="Content-Type" content="text/html; charset=us-ascii">
   <LINK REL="Previous"  HREF="012706.html">
   <LINK REL="Next"  HREF="012714.html">
 </HEAD>
 <BODY BGCOLOR="#ffffff">
   <H1>[antlr-interest] Fortran grammars</H1>
    <B>Andy Tripp</B> 
    <A HREF="mailto:antlr-interest%40antlr.org?Subject=%5Bantlr-interest%5D%20Fortran%20grammars&In-Reply-To="
       TITLE="[antlr-interest] Fortran grammars">atripp at comcast.net
       </A><BR>
    <I>Mon Jun 13 15:44:48 PDT 2005</I>
    <P><UL>
        <LI>Previous message: <A HREF="012706.html">[antlr-interest] What does the 'interactive' option mean?
</A></li>
        <LI>Next message: <A HREF="012714.html">[antlr-interest] Fortran grammars
</A></li>
         <LI> <B>Messages sorted by:</B> 
              <a href="date.html#12697">[ date ]</a>
              <a href="thread.html#12697">[ thread ]</a>
              <a href="subject.html#12697">[ subject ]</a>
              <a href="author.html#12697">[ author ]</a>
         </LI>
       </UL>
    <HR>  
<!--beginarticle-->
<PRE>I had started a F77 ANTLR grammar, but never finished.
This might make a decent starting point. I was going from the
HP F77 manual that came with their RTE-A operating system.
Andy

--------------------------------------------------------------

/** Fortran Recognizer
 *
 * Run 'java Main [-showtree] directory-full-of-java-files'
 *
 * [The -showtree option pops up a Swing frame that shows
 *  the AST constructed from the parser.]
 *
 * Run 'java Main &lt;directory full of java files&gt;'
 *
 * Contributing authors:
 *        Andy Tripp          <A HREF="http://www.antlr.org/mailman/listinfo/antlr-interest">atripp at comcast.net</A>
 */

class FortranRecognizer extends Parser;
options {
    k = 2;                           // two token lookahead
    exportVocab=Fortran;                // Call its vocabulary &quot;Fortran&quot;
    codeGenMakeSwitchThreshold = 2;  // Some optimizations
    codeGenBitsetTestThreshold = 3;
    defaultErrorHandler = false;     // Don't generate parser error handlers
    buildAST = true;
}

tokens { DOT; MIL_STD_OCTAL; MIL_STD_HEX; NUM_REAL; NUM_BINARY;
ARRAY_ELEMENT_NAME; SUBSTRING_NAME;
}

executableProgram
    : topStatement
      NEWLINE
      statementList
      &quot;END&quot;
    ;

topStatement
    : programStatement
    | functionStatement
    | subroutineStatement
    | blockDataStatement
    ;

statementList
    :    statement
        (NEWLINE CONTINUATION statement )*
    ;

/* 6 and 7 */
statement
    : includeStatement
    | formatStatement
    | entryStatement
    | parameterStatement
    | implicitStatement
    | dataStatement
    | statementFunctionStatement

    | dimensionStatement
    | equivalenceStatement
    | commonStatement
    | typeStatement
    | externalStatement
    | intrinsicStatement
    | saveStatement
    | emaStatement

    | assignmentStatement
    | gotoStatement
    | ifStatement
    | elseStatement
    | endIfStatement
    | doStatement
    | endDoStatement
    | continueStatement
    | stopStatement
    | pauseStatement
    | writeStatement
    | printStatement
    | decodeStatement
    | encodeStatement
    | rewindStatement
    | backspaceStatement
    | endFileStatement
    | openStatement
    | closeStatement
    | inquireStatement
    | callStatement
    | returnStatement
    ;


/* not in syntax chart */
includeStatement
    : &quot;INCLUDE&quot; STRING_CONSTANT
    ;

/* 8 */
programStatement
    : &quot;PROGRAM&quot; SNAME
        ( LPAREN
          ( INTEGER_CONSTANT (COMMA! INTEGER_CONSTANT)* )?
          RPAREN )?
        ( COMMA PROCESSOR_STRING )*
    ;

/* 9, 11, and 14 */
entryStatement
    : &quot;ENTRY&quot; SNAME
      ( options { generateAmbigWarnings=false; }:
       (functionArgumentList)*
      | subroutineArgumentList
      )
    ;

/* 10 */
functionStatement
    :    (
          &quot;INTEGER&quot;
        | &quot;REAL&quot;
        | &quot;DOUBLE PRECISION&quot;
        | &quot;COMPLEX&quot;
        | &quot;DOUBLE COMPLEX&quot;
        | &quot;LOGICAL&quot;
        | &quot;CHARACTER&quot;
        )
        ( STAR lenSpecification )?
        &quot;FUNCTION&quot;
        ( COMMA PROCESSOR_STRING )?
    ;


/* 12 */
functionArgumentList
    : LPAREN
      ( SNAME ( COMMA SNAME)* )?
      RPAREN
    ;

/* 13 */
subroutineStatement
    :    &quot;SUBROUTINE&quot; SNAME
        (subroutineArgumentList)?
        ( COMMA PROCESSOR_STRING )?
    ;

/* 15 */
subroutineArgumentList
    : LPAREN
      ( (SNAME | STAR) ( COMMA (SNAME | STAR))* )?
      RPAREN
    ;

/* 16 */
blockDataStatement
    :    &quot;BLOCK DATA&quot; ( SNAME ) ?
    ;

/* 17 */
dimensionStatement
    :    &quot;DIMENSION&quot; arrayDeclarator ( COMMA arrayDeclarator )*
    ;

/* 18 */
arrayDeclarator
    : SNAME
      LPAREN
      arrayDeclarator2 (COMMA arrayDeclarator2)*
      (STAR)?
      RPAREN
    ;

arrayDeclarator2
    : (dimBoundExpression (COLON dimBoundExpression)? )?
    ;

/* 19,20 */
equivalenceStatement
    : &quot;EQUIVALENCE&quot;
      equivalenceStatement2 (COMMA equivalenceStatement2)*
    ;

equivalenceStatement2
    : LPAREN
      name (COMMA name)*
      RPAREN
    ;

/* 21 */
commonStatement
    : &quot;COMMON&quot;
      common2 (COMMA common2)*
    ;

common2
options { generateAmbigWarnings=false; }
    : (DIV SNAME DIV)?
      ( arrayDeclarator | SNAME)
      ( options { generateAmbigWarnings=false; }:
        COMMA (arrayDeclarator | SNAME) )*
    ;


/* 22 */
typeStatement
    :    ( &quot;INTEGER&quot;
        | &quot;REAL&quot;
        | &quot;DOUBLE PRECISION&quot;
        | &quot;COMPLEX&quot;
        | &quot;DOUBLE COMPLEX&quot;
        | &quot;LOGICAL&quot;
        )
        (STAR lenSpecification (COMMA)? )?
        type2 (COMMA type2)*

    | &quot;CHARACTER&quot; (STAR lenSpecification (COMMA)? )?
      type2 (STAR lenSpecification)?
      (COMMA type2 (STAR lenSpecification))?
    ;

type2
    : SNAME
    | arrayDeclarator
    ;

/* 23 */
implicitStatement
    : &quot;IMPLICIT&quot; ( &quot;NONE&quot; | implicit2 (COMMA implicit2)* )
    ;

implicit2
    : ( &quot;INTEGER&quot;
            | &quot;REAL&quot;
            | &quot;DOUBLE PRECISION&quot;
            | &quot;COMPLEX&quot;
            | &quot;DOUBLE COMPLEX&quot;
            | &quot;LOGICAL&quot;
            | &quot;CHARACTER&quot;
        )
        (STAR lenSpecification)?
        LPAREN
        // DASH_STRING (COMMA DASH_STRING)*
        SNAME (COMMA SNAME)*
        PAREN
    ;


/* 24 */
lenSpecification
    : ( LPAREN STAR RPAREN)
    | INTEGER_CONSTANT
    | ( LPAREN INTEGER_CONSTANT RPAREN)
    ;

/* 25 */
parameterStatement
    : &quot;PARAMETER&quot;
      LPAREN
      ( SNAME EQUALS constantExpression )
      ( COMMA SNAME EQUALS constantExpression )*
      RPAREN
    ;

/* 26 */
externalStatement
    : &quot;EXTERNAL&quot;
      SNAME         // procedure_name or block_data_subprogram_name
      (COMMA SNAME)*
    ;

/* 27 */
intrinsicStatement
    : &quot;INTRINSIC&quot;
      SNAME            // functionName
      (COMMA SNAME)*
    ;

/* 28 */
saveStatement
    : &quot;SAVE&quot;
      (SNAME | DIV SNAME DIV)
      ( COMMA (SNAME | DIV SNAME DIV) )*
    ;


/* 29 */
emaStatement
    : &quot;EMA&quot; SNAME (COMMA SNAME)*
    ;

/* 30 */
dataStatement
    : &quot;DATA&quot; data1 (COMMA data1)*
    ;

data1
    : data2 (COMMA data2)*
      DIV
      data3 (COMMA data3)*
      DIV
    ;

data2
    : name
    | dataImpliedDoList
    ;

data3
    : ( ( INTEGER_CONSTANT | SNAME) STAR )?
      ( constant | SNAME )
    ;

/* 31 */
dataImpliedDoList
    : LPAREN
      dataImplied2 (COMMA dataImplied2)*
      SNAME EQUALS
      arithmeticExpression (COMMA arithmeticExpression)*
      RPAREN
    ;

dataImplied2
    : name (COMMA dataImpliedDoList)*
    ;

/* 34 */
assignmentStatement
    : name EQUALS expression
    | &quot;ASSIGN&quot; label &quot;TO&quot; SNAME
    ;

/* 35 */
gotoStatement
    : &quot;GO TO&quot;
      (unconditionalGoto | computedGoto | assignedGoto)
    ;

/* 36 */
unconditionalGoto
    : INTEGER_CONSTANT
    ;

/* 37 */
computedGoto
    : LPAREN
      label (COMMA label)*
      RPAREN
      (COMMA)?
      arithmeticExpression
    ;

/* 38 */
assignedGoto
    : SNAME
      ((COMMA)? LPAREN label (COMMA label)* RPAREN)?
    ;

/* 39, 40, 42 */
ifStatement
    : &quot;IF&quot; LPAREN arithmeticExpression RPAREN
      (
          (label COMMA label COMMA label)
// TMP        | (statement | &quot;THEN&quot;)
      )
    ;

/* 42, 43 */
elseStatement
    : &quot;ELSE&quot; (ifStatement)?
    ;

/* 44 */
endIfStatement
    : &quot;END IF&quot;
    ;

/* 45, 46, 47 */
doStatement
    : &quot;DO&quot; (label (COMMA)?)?
        (
            (SNAME EQUALS arithmeticExpression (COMMA 
arithmeticExpression)*)
            | (&quot;WHILE&quot; LPAREN arithmeticExpression RPAREN)
        )
    ;


/* 48 */
endDoStatement
    : &quot;END DO&quot;
    ;

/* 49 */
continueStatement
    : &quot;CONTINUE&quot;
    ;

/* 50 */
stopStatement
    : &quot;STOP&quot; (INTEGER_CONSTANT | characterExpression)?
    ;

/* 51 */
pauseStatement
    : &quot;PAUSE&quot; (INTEGER_CONSTANT | characterExpression)?
    ;

/* 52 */
writeStatement
    : &quot;WRITE&quot; LPAREN controlInfoList RPAREN (ioList)?
    ;

/* 53 */
readStatement
    : &quot;READ&quot;
        (
          options { generateAmbigWarnings=false; }:
          LPAREN controlInfoList RPAREN (ioList)?
        | (formatIdentifier (COMMA ioList)?)
        )
    ;

/* 54 */
printStatement
    : &quot;PRINT&quot; formatIdentifier (COMMA ioList)?
    ;

/* 55 */
decodeStatement
    : &quot;DECODE&quot; encodeDecode1
    ;
   
/* 56 */
encodeStatement
    : &quot;ENCODE&quot; encodeDecode1
    ;
   
encodeDecode1
    : LPAREN
      arithmeticExpression COMMA formatIdentifier COMMA SNAME      
      (encodeDecode2)*
      RPAREN
      (ioList)?
    ;
     
encodeDecode2
    : COMMA
        (
          (&quot;ERR&quot; EQUALS LABEL)
        | (&quot;IOSTAT&quot; EQUALS name)
        )
    ;


/* 57 */
controlInfoList
    : unitIdentifier (COMMA controlInfoList2)*
    ;

controlInfoList2
    : &quot;FMT&quot; EQUALS formatIdentifier
    | &quot;UNIT&quot; EQUALS unitIdentifier
    | &quot;REC&quot; EQUALS arithmeticExpression
    | &quot;ZBUF&quot; EQUALS name
    | &quot;ZLEN&quot; EQUALS arithmeticExpression
    | &quot;END&quot; EQUALS label
    | &quot;ERR&quot; EQUALS label
    | &quot;IOSTAT&quot; EQUALS name
    | formatIdentifier
    ;

/* 58 */
unitIdentifier
    : arithmeticExpression
        (
            // TMP: dont support for now: (APOSTROPHE arithmeticExpression)
          (COLON arithmeticExpression) (COLON arithmeticExpression)?
        )
    | STAR
    ;

/* 59 */
ioList
//    : ioList2 (COMMA ioList2)*
    : expression (COMMA expression)*
    ;

/* don't support for now:
ioList2
    : expression
    | ioImpliedDoList
    ;
*/

/* 60 */
/* don't support for now:
ioImpliedDoList
    : LPAREN ioList COMMA SNAME EQUALS arithmeticExpression
      (COMMA arithmeticExpression)+
      RPAREN
    ;
*/

/* 61 */
openStatement
    : &quot;OPEN&quot; LPAREN open2 (COMMA open2)* RPAREN
    ;

open2
    : &quot;UNIT&quot; EQUALS arithmeticExpression
    | &quot;ERR&quot; EQUALS characterExpression
    | &quot;FILE&quot; EQUALS characterExpression
    | &quot;STATUS&quot; EQUALS characterExpression
    | &quot;ACCESS&quot; EQUALS characterExpression
    | &quot;FORM&quot; EQUALS characterExpression
    | &quot;RECL&quot; EQUALS arithmeticExpression
    | &quot;BLANK&quot; EQUALS characterExpression
    | &quot;MAXREC&quot; EQUALS arithmeticExpression
    | &quot;USE&quot; EQUALS characterExpression
    | &quot;NODE&quot; EQUALS arithmeticExpression
    | &quot;BUFSIZ&quot; EQUALS arithmeticExpression
    | &quot;IOSTAT&quot; EQUALS name
    | arithmeticExpression
    ;

/* 62 */
closeStatement
    : &quot;CLOSE&quot; LPAREN close2 (COMMA close2)* RPAREN
    ;

close2
    : &quot;UNIT&quot; EQUALS arithmeticExpression
    | &quot;ERR&quot; EQUALS label
    | &quot;STATUS&quot; EQUALS characterExpression
    | &quot;IOSTAT&quot; EQUALS name
    | arithmeticExpression
    ;


/* 63 */
inquireStatement
    : &quot;INQUIRE&quot; LPAREN inquire2 (COMMA inquire2)* RPAREN
    ;

inquire2
    : &quot;UNIT&quot; EQUALS arithmeticExpression
    | &quot;FILE&quot; EQUALS characterExpression
    | &quot;ERR&quot; EQUALS label
    | (
        &quot;IOSTAT&quot; | &quot;EXIST&quot; | &quot;OPENED&quot; | &quot;NUMBER&quot; | &quot;NAMED&quot; | &quot;NAME&quot; |
        &quot;ACCESS&quot; | &quot;SEQUENTIAL&quot; | &quot;DIRECT&quot; | &quot;FORM&quot; | &quot;FORMATTED&quot; |
        &quot;UNFORMATTED&quot; | &quot;RECL&quot; | &quot;NEXTREC&quot; | &quot;BLANK&quot; | &quot;MAXREC&quot; |
        &quot;USE&quot; | &quot;NODE&quot;
      )  name
    ;

/* 64 */
backspaceStatement
    : &quot;BACKSPACE&quot;
    ;

/* 65 */
endFileStatement
    : &quot;ENDFILE&quot;
    ;

/* 66 */
rewindStatement
    : &quot;REWIND&quot;
        (
          // when we see LPAREN then arithmeticExpression, it's ok
          // to just match rewind2
          options { generateAmbigWarnings=false; }:
          ( LPAREN rewind2 (COMMA rewind2)* RPAREN )
          | arithmeticExpression
        )
    ;

rewind2
    : &quot;UNIT&quot; EQUALS arithmeticExpression
    | &quot;ERR&quot; EQUALS label
    | &quot;IOSTAT&quot; EQUALS name
    | arithmeticExpression
    ;


/* 67 */
formatIdentifier
    : label
    | characterExpression
    | STAR
    ;

/* 68, 69, 70, 71-79 */
formatStatement
    : FORMAT_STATEMENT
    ;

/* 80 */
statementFunctionStatement
    : LPAREN (SNAME (COMMA SNAME)* )? RPAREN EQUALS expression
    ;

/* 81 */
callStatement
    : &quot;CALL&quot; SNAME ( LPAREN (call2 (COMMA call2)*)? )? RPAREN
    ;

call2
    : expression
    | PROCESSOR_STRING  // longHollerithConst
    | STAR label
    ;

/* 82 */
returnStatement
    : &quot;RETURN&quot; arithmeticExpression
    ;

/* 83 */
functionReference
    : SNAME LPAREN (functionRef2 (COMMA functionRef2)*)? RPAREN
    ;

functionRef2
    : expression
    | PROCESSOR_STRING  // longHollerithConst
    ;

/* 84 */
expression
    : characterExpression (relOp characterExpression)*
    ;

/* 85 */
constantExpression
    : expression
    ;

/* 86, 87, 88 */
// integerExpression, logicalExpression, combined into arithmeticExpression
// combined into just expression

characterExpression
    : arithmeticExpression ( (SLASH_SLASH) arithmeticExpression )*
    ;

arithmeticExpression
    : additiveExpression ( (AND|OR|EQV|NEQV|XOR|EOR) additiveExpression )*
    ;

additiveExpression
    : multiplicativeExpression ( (PLUS | MINUS) multiplicativeExpression )*
    ;

multiplicativeExpression
    : exponentiationExpression ( (STAR | DIV) exponentiationExpression )*
    ;

exponentiationExpression
    : unaryExpression ( (EXPONENT) unaryExpression )*
    ;


unaryExpression
    options { generateAmbigWarnings=false; }// functionReference before name
    : PLUS unaryExpression
    | MINUS unaryExpression
    | NOT unaryExpression
    | functionReference
    | name
    | LPAREN arithmeticExpression RPAREN
    | logicalConstant
    | CHARACTER_CONSTANT
    ;

/* 91 */
dimBoundExpression
    : unaryExpression ((PLUS|MINUS|DIV|EXPONENT) unaryExpression )*
    ;

/* 92 */
// characterExpression moved above arithmeticExpression

/* No need for 93: characterConstExpression */

/* 96 */
// relationalExpression combined with expression

/* 97 */
relOp
    : LT
    | LE
    | EQ
    | NE
    | GT
    | GE
    ;


/* 98, 99 */
name
    : sname:SNAME
      (LPAREN (arithmeticExpression)?
              (
              (COMMA arithmeticExpression)+
                {#sname.setType(ARRAY_ELEMENT_NAME);}

              | COLON (arithmeticExpression)?
                {#sname.setType(SUBSTRING_NAME);}
            )
      RPAREN {#sname.setType(SUBSTRING_NAME);}
      )
    ;

/* 110 */
constant
    : logicalConstant
    | CHARACTER_CONSTANT
    | INTEGER_CONSTANT
    | NUM_REAL
    | MIL_STD_OCTAL
    | MIL_STD_HEX
    | NUM_BINARY
    ;

/* 121 */
logicalConstant
    : &quot;.TRUE.&quot;
    | &quot;.FALSE.&quot;
    ;


/* 123 */
label
    : INTEGER_CONSTANT
    ;

     
    
//----------------------------------------------------------------------------
    // The Fortran scanner
    
//----------------------------------------------------------------------------
class FortranLexer extends Lexer;

options {
    exportVocab=Fortran;      // call the vocabulary &quot;Fortran&quot;
    testLiterals=false;    // don't automatically test for literals
    k=4;                   // four characters of lookahead
    charVocabulary='\u0003'..'\u7FFE';
}



EQUALS            :    '='        ;
LPAREN            :    '('        ;
RPAREN            :    ')'        ;
COLON            :    ':'        ;
COMMA            :    ','        ;
DIV                :    '/'        ;
STAR            :    {getColumn() != 1}? '*'        ;
PLUS            :    '+'        ;
MINUS            :    '-'        ;
LNOT            :    &quot;.NOT.&quot;        ;
LAND            :    &quot;.AND.&quot;        ;
LOR                :    &quot;.OR.&quot;    ;
EQV                :    &quot;.EQV.&quot;    ;
NEQV            :    &quot;.NEQV.&quot;    ;
XOR                :    &quot;.XOR.&quot;    ;
EOR                :    &quot;.EOR.&quot;    ;
EXP                :    {getColumn() != 1}? &quot;**&quot;        ;
SLASH_SLASH        :    &quot;//&quot;        ;
LT                :    &quot;.LT.&quot;        ;
LE                :    &quot;.LE.&quot;        ;
GT                :    &quot;.GT.&quot;        ;
GE                :    &quot;.GE.&quot;        ;
NE                :    &quot;.NE.&quot;        ;
EQ                :    &quot;.EQ.&quot;        ;
CONTINUATION    :    &quot;&amp;&quot;        ;


// Whitespace -- ignored
WS    :    (    ' '
        |    '\t'
        |    '\f'
            // handle newlines
        |    (    options {generateAmbigWarnings=false;}
            :    &quot;\r\n&quot;  // Evil DOS
            |    '\r'    // Macintosh
            |    '\n'    // Unix (the right way)
            )
            { newline(); }
        )+
        { _ttype = Token.SKIP; }
    ;

// Single-line comments
END_COMMENT
    :    &quot;!&quot;
        (~('\n'|'\r'))* ('\n'|'\r'('\n')?)?
        {$setType(Token.SKIP); newline();}
    ;

SL_COMMENT
    :    {getColumn() == 1}? ('C' | '*')
        (~('\n'|'\r'))* ('\n'|'\r'('\n')?)?
        {$setType(Token.SKIP); newline();}
    ;

/* 122 */
CHARACTER_CONSTANT
    :    '\'' ( ~('\'') )* '\''
    ;

// string literals
STRING_LITERAL
    :    '&quot;' (~('&quot;'|'\\'|'\n'|'\r'))* '&quot;'
    ;


LOGICAL_CONSTANT
    : &quot;.TRUE.&quot; | &quot;.FALSE.&quot;
    ;


// hexadecimal digit (again, note it's protected!)
protected
OCTAL_DIGIT
    :    ('0'..'7')
    ;

// hexadecimal digit (again, note it's protected!)
protected
HEX_DIGIT
    :    ('0'..'9'|'A'..'F'|'a'..'f')
    ;

// an identifier.  Note that testLiterals is set to true!  This means
// that after we match the rule, we look in the literals table to see
// if it's a literal or really an identifer
SNAME
    options {testLiterals=true;}
    :    {getColumn() != 1 &amp;&amp; LA(2) != '\''}? 
('a'..'z'|'A'..'Z'|'_'|'$') ('a'..'z'|'A'..'Z'|'_'|'0'..'9'|'$'|'-')*
    ;


// a numeric literal
INTEGER_CONSTANT
    :   '.' {_ttype = DOT;}
            (    ('0'..'9')+ (EXPONENT)?
                {
                    _ttype = NUM_REAL;
                }
            )?

/* 32 */
    |    ( 'O' '\'' (OCTAL_DIGIT)+ '\'' ) { _ttype = MIL_STD_OCTAL; }
/* 33 */
    |    ( 'Z' '\'' (HEX_DIGIT)+ '\'' ) { _ttype = MIL_STD_HEX; }
    |    ('0'..'9')+
        (
            '.' ('0'..'9')* (EXPONENT)? { _ttype = NUM_REAL; }
        |    ('I'|'J')? { _ttype = INTEGER_CONSTANT; }
        |    ('B') { _ttype = NUM_BINARY; }
        )
    ;


// a couple protected methods to assist in matching floating point numbers
protected
EXPONENT
    :    ('e'|'E') ('+'|'-')? ('0'..'9')+
    |    ('d'|'D') ('+'|'-')? ('0'..'9')+
    ;

// for now, accept anything within parens:
FORMAT_STATEMENT
    : &quot;FORMAT&quot; '(' (~')')* '('
    ;

protected
NEWLINE
    : ('\r' | '\n' | '\r' '\n')
    ;



</PRE>














<!--endarticle-->
    <HR>
    <P><UL>
        <!--threads-->
	<LI>Previous message: <A HREF="012706.html">[antlr-interest] What does the 'interactive' option mean?
</A></li>
	<LI>Next message: <A HREF="012714.html">[antlr-interest] Fortran grammars
</A></li>
         <LI> <B>Messages sorted by:</B> 
              <a href="date.html#12697">[ date ]</a>
              <a href="thread.html#12697">[ thread ]</a>
              <a href="subject.html#12697">[ subject ]</a>
              <a href="author.html#12697">[ author ]</a>
         </LI>
       </UL>

<hr>
<a href="http://www.antlr.org/mailman/listinfo/antlr-interest">More information about the antlr-interest
mailing list</a><br>
</body></html>
