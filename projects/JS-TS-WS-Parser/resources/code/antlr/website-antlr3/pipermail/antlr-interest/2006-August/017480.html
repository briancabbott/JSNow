<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 3.2//EN">
<HTML>
 <HEAD>
   <TITLE> [antlr-interest] v3: conformance of current Java 1.5 grammar
   </TITLE>
   <LINK REL="Index" HREF="index.html" >
   <LINK REL="made" HREF="mailto:antlr-interest%40antlr.org?Subject=%5Bantlr-interest%5D%20v3%3A%20conformance%20of%20current%20Java%201.5%20grammar&In-Reply-To=B63C83C0-A2C0-4B1B-B08F-2D4DFD09A26A%40cs.usfca.edu">
   <META NAME="robots" CONTENT="index,nofollow">
   <META http-equiv="Content-Type" content="text/html; charset=us-ascii">
   <LINK REL="Previous"  HREF="017476.html">
   <LINK REL="Next"  HREF="017477.html">
 </HEAD>
 <BODY BGCOLOR="#ffffff">
   <H1>[antlr-interest] v3: conformance of current Java 1.5 grammar</H1>
    <B>Dieter Frej</B> 
    <A HREF="mailto:antlr-interest%40antlr.org?Subject=%5Bantlr-interest%5D%20v3%3A%20conformance%20of%20current%20Java%201.5%20grammar&In-Reply-To=B63C83C0-A2C0-4B1B-B08F-2D4DFD09A26A%40cs.usfca.edu"
       TITLE="[antlr-interest] v3: conformance of current Java 1.5 grammar">dieter_frej at gmx.net
       </A><BR>
    <I>Sat Aug 26 08:47:48 PDT 2006</I>
    <P><UL>
        <LI>Previous message: <A HREF="017476.html">[antlr-interest] v3: conformance of current Java 1.5 grammar
</A></li>
        <LI>Next message: <A HREF="017477.html">[antlr-interest] Exception at compile
</A></li>
         <LI> <B>Messages sorted by:</B> 
              <a href="date.html#17480">[ date ]</a>
              <a href="thread.html#17480">[ thread ]</a>
              <a href="subject.html#17480">[ subject ]</a>
              <a href="author.html#17480">[ author ]</a>
         </LI>
       </UL>
    <HR>  
<!--beginarticle-->
<PRE>my email webfront seemed to have messed it up.Sorry for that!

Around 73% of the rules in the grammar are like the rules as they are in
the Java Language Specification. I think it is a good value, but judge
for yourself.
Parser: 99 rules of 130 (~ 76%)
Lexer: 11 rules of 20 (~ 55%)
The attache text file containes the rules (taken from the grammar) and
the specification page number if they match.

I also attached the grammar again. I would like to get exceptions thrown
by my JavaParser so that I can analyse them. Therefore, I probably need
to overwrite either BaseRecognizer.recover() or 
BaseRecognizer.reportError(). Then I can start parsing the JCK again. I
would like to do this a) to compare that to the v2 Java 1.5 grammar and
b) compare it to the information provided by the JCK.

- Didi



Terence Parr wrote:
&gt;<i> 
</I>&gt;<i> On Aug 25, 2006, at 10:39 AM, Dieter Frej wrote:
</I>&gt;<i> 
</I>&gt;&gt;<i> Hi everbody,
</I>&gt;<i> 
</I>&gt;<i> 
</I>&gt;<i> Hi Dieter,
</I>&gt;<i> 
</I>&gt;&gt;<i> some time ago, I had some spare time and I checked the conformance
</I>&gt;&gt;<i> of the current Java 1.5 grammar to the Java Language Specification 3.
</I>&gt;&gt;<i> And I was really surprised.
</I>&gt;<i> 
</I>&gt;<i> 
</I>&gt;<i> In a good way or a bad way?
</I>&gt;<i> 
</I>&gt;&gt;<i> I attached a file listing the rules and
</I>&gt;&gt;<i> their corresponding page number in the jls3 pdf.
</I>&gt;<i> 
</I>&gt;<i> 
</I>&gt;<i> I don't see any information in that conformance.txt file.  Did you  send 
</I>&gt;<i> the right file?
</I>&gt;<i> 
</I>&gt;&gt;<i> To increase the
</I>&gt;&gt;<i> conformity I renamed some rules.
</I>&gt;&gt;<i>
</I>&gt;&gt;<i> I also attached the changed java grammar. So far, I had no time to
</I>&gt;&gt;<i> look into overwriting the method,
</I>&gt;<i> 
</I>&gt;<i> 
</I>&gt;<i> Which method?
</I>&gt;<i> 
</I>&gt;&gt;<i> so that exceptions are not
</I>&gt;&gt;<i> catched by antlr. If this is done (has anyone does this yet?), I can
</I>&gt;&gt;<i> start compiling the JCK again...
</I>&gt;<i> 
</I>&gt;<i> 
</I>&gt;<i> Thanks for putting time into this but I must admit I cannot parse  this 
</I>&gt;<i> e-mail and determine what exactly you have and don't  have...sorry! can 
</I>&gt;<i> you try to explain in more detail?
</I>&gt;<i> 
</I>&gt;<i> Ter
</I>&gt;<i> 
</I>&gt;<i> 
</I>
-------------- next part --------------
compilationUnit

packageDeclaration

// jls3, p. 592
importDeclaration
	
// jls3, p. 592
typeDeclaration

// jls3, p. 592
classOrInterfaceDeclaration
	
// jls3, p. 592
classDeclaration
	
// jls3, p. 593
normalClassDeclaration
	
// jls3, p. 593
typeParameters

// jls3, p. 593
typeParameter
	
// jls3, p. 593
bound

// jls3, p. 593
enumDeclaration
	
// jls3, p. 593
enumBody

// jls3, p. 593
enumConstants
	
// jls3, p. 593
enumConstant
	
// jls3, p. 593
enumBodyDeclarations
	
// jls3, p. 593
interfaceDeclaration
	
// jls3, p. 593
normalInterfaceDeclaration
	
// jls3, p. 593
typeList
	
// jls3, p. 594
classBody
	
// jls3, p.  594
interfaceBody

// jls3, p. 594
classBodyDeclaration
	
memberDecl
	
// jls3, p. 595
genericMethodOrConstructorDecl
	
// jls3, p. 595
genericMethodOrConstructorRest

methodDeclaration

fieldDeclaration
	
// jls3, p. 595
interfaceBodyDeclaration

// jls3, p. 595
interfaceMemberDecl
	
// jls3, p. 595
interfaceMethodOrFieldDecl
	
// jls3, p. 595
interfaceMethodOrFieldRest
	
// jls3, p. 595
methodDeclaratorRest
	
// jls3, p. 595
voidMethodDeclaratorRest
	
// jls3, p. 595
interfaceMethodDeclaratorRest
	
// jls3, p. 595
interfaceGenericMethodDecl
	
// jls3, p. 595
voidInterfaceMethodDeclaratorRest
	
// jls3, p. 596
constructorDeclaratorRest

// jls3, p. 592
constantDeclarator
	
// jls3, p. 592
variableDeclarators

// jls3, p. 592
variableDeclaratorsRest
	
// jls3, p. 592
variableDeclarator
	
variableDeclaratorRest

// jls3, p. 592
constantDeclaratorsRest

// jls3, p. 592
constantDeclaratorRest

// jls3, p. 592
variableDeclaratorId

// jls3, p. 589
variableInitializer

// jls3, p. 589
arrayInitializer

// jls3, p. 591
modifier

packageOrTypeName

// jls3, p. 596
enumConstantName

// jls3, p. 126
typeName

type

// jls3, p. 589
basicType

// jls3, p. 211
variableModifier

// jls3, p. 586
typeArguments
	
// jls3, p. 586
typeArgument
	
// jls3, p. 596
qualifiedIdentifierList
	
// jls3, p. 596
formalParameters

// jls3, p. 596	
formalParameterDecls
	
// jls3, p. 596
formalParameterDeclsRest
	
// jls3, p. 596
methodBody

// jls3, p. 585
qualifiedIdentifier
	
// jls3, p. 586
literal	

// jls3, p. 22
integerLiteral

// jls3, p. 26
booleanLiteral

// jls3, p. 591
annotations

// jls3, p. 591
annotation
	
// jls3, p. 591
elementValue
	
// jls3, p. 591
elementValueArrayInitializer
	
// jls3, p. 593
annotationTypeDeclaration
	
// jls3, p. 593
annotationTypeBody
	
// jls3, p. 594 (left-recursive resolved)
annotationTypeElementDeclarations

// jls3, p. 594
annotationTypeElementDeclaration
	
// jls3, p. 594
annotationTypeElementRest
	
// jls3, p. 594
annotationMethodOrConstantRest
	
// jls3, p. 594
annotationMethodRest
 	
// jls3, p. 594
annotationConstantRest
 	
// jls3, p. 594
defaultValue

// STATEMENTS / BLOCKS
// jls3, p. 589
block
	
// jls3, p. 590
blockStatements
	
// jls3, p. 590
blockStatement
	
// jls3, p. 590
localVariableDeclarationStatement
	
statement
	
// jls3, p. 590
catches
	
// jls3, p. 590
catchClause

// jls3, p. 211
formalParameter
	
// jls3, p. 590	
switchBlockStatementGroups
	
// jls3, p. 590
switchBlockStatementGroup
	
// jls3, p. 590
switchLabel
	
// jls3, p. 591
moreStatementExpressions

// jls3, p. 591
forControl

forInit
	
// jls3, p. 591
forVarControl

// jls3, p. 591
forVarControlRest

// jls3, p. 384
forUpdate

// EXPRESSIONS
// jls3, p. 589
parExpression

// jls3, p. 384
statementExpressionList
	
// jls3, p. 586
statementExpression

// jls3, p. 586	
constantExpression
	
expression
	
// jls3, p. 586
assignmentOperator

conditionalExpression

conditionalOrExpression

conditionalAndExpression

inclusiveOrExpression

exclusiveOrExpression

andExpression

equalityExpression

instanceOfExpression

relationalExpression
	
relationalOp

shiftExpression

shiftOp

additiveExpression

multiplicativeExpression
	
unaryExpression

unaryExpressionNotPlusMinus

castExpression

// jls3, p. 587
primary

identifierSuffix
	
// jls3, p. 589
creator

createdName
	
// jls3, p. 589
innerCreator

// jls3, p. 589
arrayCreatorRest

// jls3, p. 589
classCreatorRest
	
// jls3, p. 588
explicitGenericInvocation
	
// jls3, p. 588
nonWildcardTypeArguments
	
// jls3, p. 588
explicitGenericInvocationSuffix
	
// jls3, p. 588
selector
	
// jls3, p. 589
superSuffix

arguments



// LEXER
HexIntegerLiteral

DecimalIntegerLiteral

OctalIntegerLiteral

// jls3, p. 23
fragment
HexDigit

// jls3, p. 22
fragment
IntegerTypeSuffix

FloatingPointLiteral

fragment
Exponent

// jls3, p. 25
fragment
FloatTypeSuffix

// jls3, p. 27
CharacterLiteral

// jls3, p. 28
StringLiteral

// jls3, p. 30
fragment
EscapeSequence

// jls3, p. 30
fragment
OctalEscape

// jls3, p. 15
fragment
UnicodeEscape

ENUM
	
Identifier 

// jls3, p. 20
fragment
JavaLetter

// jls3, p. 20
fragment
JavaLetterOrDigit

// jls3, p. 17
WS

Comment

EndOfLineComment
-------------- next part --------------
/**
 * A Java 1.5 grammar for ANTLR v3 derived from the spec
 *
 * This is a very close representation of the spec; the changes
 * are comestic (remove left recursion) and also fixes (the spec
 * isn't exactly perfect). I have run this on the 1.4.2 source
 * and some nasty looking enums from 1.5, but have not really
 * tested for 1.5 compatibility.
 *
 * I built this with:
 * java -Xmx300M org.antlr.Tool java.g
 * and got two errors that are ok:
 * java.g:691:9: Decision can match input such as
 *   &quot;'0'..'9'{'E', 'e'}{'+', '-'}'0'..'9'{'D', 'F', 'd', 'f'}&quot;
 *   using multiple alternatives: 3, 4
 * As a result, alternative(s) 4 were disabled for that input
 * java.g:734:35: Decision can match input such as &quot;{'$', 'A'..'Z',
 *   '_', 'a'..'z', '\u00C0'..'\u00D6', '\u00D8'..'\u00F6',
 *   '\u00F8'..'\u1FFF', '\u3040'..'\u318F', '\u3300'..'\u337F',
 *   '\u3400'..'\u3D2D', '\u4E00'..'\u9FFF', '\uF900'..'\uFAFF'}&quot;
 *   using multiple alternatives: 1, 2
 * As a result, alternative(s) 2 were disabled for that input
 *
 * You can turn enum on/off as a keyword :)
 *
 * Version 1.0 -- initial release July 5, 2006 (requires 3.0b2)
 *
 * Primary author: Terence Parr, July 2006
 */
grammar JavaParser;
options {
	k=2;
	backtrack=true;
	memoize=true;
}

@lexer::members {
	protected boolean enumIsKeyword = false;
}

// starting point for parsing a java file
compilationUnit
	:	annotations?
		packageDeclaration?
      		importDeclaration*
      		typeDeclaration*
	;

packageDeclaration
	:	'package' qualifiedIdentifier ';'
	;

importDeclaration
	:	'import' 'static'? Identifier ('.' Identifier)* ('.' '*')? ';'
	;
	
typeDeclaration
	:	classOrInterfaceDeclaration
	|	';'
	;

classOrInterfaceDeclaration
	:	modifier* (classDeclaration | interfaceDeclaration)
	;
	
classDeclaration
	:	normalClassDeclaration
	|	enumDeclaration
	;
	
normalClassDeclaration
	:	'class' Identifier (typeParameters)?
		('extends' type)?
		('implements' typeList)?
		classBody
	;
	
typeParameters
	:	'&lt;' typeParameter (',' typeParameter)* '&gt;'
	;

typeParameter
	:	Identifier ('extends' bound)?
	;
	
bound
	:	type ('&amp;' type)*
	;

enumDeclaration
	:	ENUM Identifier ('implements' typeList)? enumBody
	;
	
enumBody
	:	'{' enumConstants? ','? enumBodyDeclarations? '}'
	;

enumConstants
	:	enumConstant (',' enumConstant)*
	;
	
enumConstant
	:	annotations? Identifier (arguments)? (classBody)?
	;
	
enumBodyDeclarations
	:	';' (classBodyDeclaration)*
	;
	
interfaceDeclaration
	:	normalInterfaceDeclaration
	|	annotationTypeDeclaration
	;
	
normalInterfaceDeclaration
	:	'interface' Identifier typeParameter? ('extends' typeList)? interfaceBody
	;
	
typeList
	:	type (',' type)*
	;
	
classBody
	:	'{' classBodyDeclaration* '}'
	;
	
interfaceBody
	:	'{' interfaceBodyDeclaration* '}'
	;

classBodyDeclaration
	:	';'
	|	'static'? block
	|	modifier* memberDecl
	;
	
memberDecl
	:	genericMethodOrConstructorDecl
	|	methodDeclaration
	|	fieldDeclaration
	|	'void' Identifier voidMethodDeclaratorRest
	|	Identifier constructorDeclaratorRest
	|	interfaceDeclaration
	|	classDeclaration
	;
	
genericMethodOrConstructorDecl
	:	typeParameters genericMethodOrConstructorRest
	;
	
genericMethodOrConstructorRest
	:	(type | 'void') Identifier methodDeclaratorRest
	|	Identifier constructorDeclaratorRest
	;

methodDeclaration
	:	type Identifier methodDeclaratorRest
	;

fieldDeclaration
	:	type variableDeclarators ';'
	;
	
interfaceBodyDeclaration
	:	';'
	|	modifier* interfaceMemberDecl
	;

interfaceMemberDecl
	:	interfaceMethodOrFieldDecl
	|	interfaceGenericMethodDecl
	|	'void' Identifier voidInterfaceMethodDeclaratorRest
	|	interfaceDeclaration
	|	classDeclaration
	;
	
interfaceMethodOrFieldDecl
	:	type Identifier interfaceMethodOrFieldRest
	;
	
interfaceMethodOrFieldRest
	:	constantDeclaratorsRest ';'
	|	interfaceMethodDeclaratorRest
	;
	
methodDeclaratorRest
	:	formalParameters ('[' ']')*
		('throws' qualifiedIdentifierList)?
		(methodBody | ';')
	;
	
voidMethodDeclaratorRest
	:	formalParameters ('throws' qualifiedIdentifierList)?
		(methodBody | ';')
	;
	
interfaceMethodDeclaratorRest
	:	formalParameters ('[' ']')* ('throws' qualifiedIdentifierList)? ';'
	;
	
interfaceGenericMethodDecl
	:	typeParameters (type | 'void') Identifier
		interfaceMethodDeclaratorRest
	;
	
voidInterfaceMethodDeclaratorRest
	:	formalParameters ('throws' qualifiedIdentifierList)? ';'
	;
	
constructorDeclaratorRest
	:	formalParameters ('throws' qualifiedIdentifierList)? methodBody
	;

constantDeclarator
	:	Identifier constantDeclaratorRest
	;
	
variableDeclarators
	:	variableDeclarator (',' variableDeclarator)*
	;

variableDeclaratorsRest
	:	variableDeclaratorRest (',' variableDeclarator)*
	;
	
variableDeclarator
	:	Identifier variableDeclaratorRest
	;
	
variableDeclaratorRest
	:	('[' ']')+ ('=' variableInitializer)?
	|	'=' variableInitializer
	|
	;

constantDeclaratorsRest
	:   constantDeclaratorRest (',' constantDeclarator)*
	;

constantDeclaratorRest
	:	('[' ']')* '=' variableInitializer
	;
	
variableDeclaratorId
	:	Identifier ('[' ']')*
	;

variableInitializer
	:	arrayInitializer
	|	expression
	;

arrayInitializer
	:	'{' (variableInitializer (',' variableInitializer)* (',')?)? '}'
	;

modifier
	:	annotation
	|	'public'
	|	'protected'
	|	'private'
	|	'static'
	|	'abstract'
	|	'final'
	|	'native'
	|	'synchronized'
	|	'transient'
	|	'volatile'
	|	'strictfp'
	;


packageOrTypeName
	:	Identifier ('.' Identifier)*
	;

enumConstantName
	:	Identifier
	;

typeName
	:	Identifier
	|	packageOrTypeName '.' Identifier
	;

type
	:	Identifier (typeArguments)? ('.' Identifier (typeArguments)?)* ('[' ']')*
	|	basicType ('[' ']')*
	;

basicType
	:	'byte'
	|	'short'
	|	'char'
	|	'int'
	|	'long'
	|	'float'
	|	'double'
	|	'boolean'
	;

variableModifier
	:	'final'
	|	annotation
	;

typeArguments
	:	'&lt;' typeArgument (',' typeArgument)* '&gt;'
	;
	
typeArgument
	:	type
	|	'?' (('extends' | 'super') type)?
	;
	
qualifiedIdentifierList
	:	qualifiedIdentifier (',' qualifiedIdentifier)*
	;
	
formalParameters
	:	'(' formalParameterDecls? ')'
	;

formalParameterDecls
	:	'final'? annotations? type formalParameterDeclsRest?
	;
	
formalParameterDeclsRest
	:	variableDeclaratorId (',' formalParameterDecls)?
	|	'...' variableDeclaratorId
	;
	
methodBody
	:	block
	;

qualifiedIdentifier
	:	Identifier ('.' Identifier)*
	;
	
literal	
	:	integerLiteral
	|	FloatingPointLiteral
	|	CharacterLiteral
	|	StringLiteral
	|	booleanLiteral
	|	'null'
	;

integerLiteral
	:	DecimalIntegerLiteral
	|	HexIntegerLiteral
	|	OctalIntegerLiteral
	;

booleanLiteral
	:	'true'
	|	'false'
	;

// ANNOTATIONS
annotations
	:	annotation+
	;

annotation
	:	'@' typeName ('(' (Identifier '=')? elementValue ')')?
	;
	
elementValue
	:	conditionalExpression
	|	annotation
	|	elementValueArrayInitializer
	;
	
elementValueArrayInitializer
	:	'{' (elementValue)? (',')? '}'
	;
	
annotationTypeDeclaration
	:	'@' 'interface' Identifier annotationTypeBody
	;
	
annotationTypeBody
	:	'{' (annotationTypeElementDeclarations)? '}'
	;
	
annotationTypeElementDeclarations
	:	(annotationTypeElementDeclaration) (annotationTypeElementDeclaration)*
	;

annotationTypeElementDeclaration
	:	(modifier)* annotationTypeElementRest
	;
	
annotationTypeElementRest
	:	type Identifier annotationMethodOrConstantRest ';'
	|	classDeclaration
	|	interfaceDeclaration
	|	enumDeclaration
	|	annotationTypeDeclaration
	;
	
annotationMethodOrConstantRest
	:	annotationMethodRest
	|	annotationConstantRest
	;
	
annotationMethodRest
 	:	'(' ')' (defaultValue)?
 	;
 	
annotationConstantRest
 	:	variableDeclarators
 	;
 	
defaultValue
 	:	'default' elementValue
 	;

// STATEMENTS / BLOCKS
block
	:	'{' blockStatements '}'
	;
	
blockStatements
	:	blockStatement*
	;
	
blockStatement
	:	localVariableDeclarationStatement
	|	classOrInterfaceDeclaration
	|	(Identifier ':')? statement
	;
	
localVariableDeclarationStatement
	:	('final')? type variableDeclarators ';'
	;
	
statement
	:	block
	|	'assert' expression (':' expression)? ';'
	|	'if' parExpression statement ('else' statement)?
	|	'for' '(' forControl ')' statement
	|	'while' parExpression statement
	|	'do' statement 'while' parExpression ';'
	|	'try' block
		(	catches 'finally' block
		|	catches
		|	'finally' block
		)
	|	'switch' parExpression '{' switchBlockStatementGroups '}'
	|	'synchronized' parExpression block
	|	'return' expression? ';'
	|	'throw' expression ';'
	|	'break' Identifier? ';'
	|	'continue' Identifier? ';'
	|	';'
	|	statementExpression ';'
	|	Identifier ':' statement
	;
	
catches
	:	catchClause (catchClause)*
	;
	
catchClause
	:	'catch' '(' formalParameter ')' block
	;

formalParameter
	:	variableModifier* type variableDeclaratorId
	;
	
switchBlockStatementGroups
	:	(switchBlockStatementGroup)*
	;
	
switchBlockStatementGroup
	:	switchLabel blockStatement*
	;
	
switchLabel
	:	'case' constantExpression ':'
	|	'case' enumConstantName ':'
	|	'default' ':'
	;
	
moreStatementExpressions
	:	(',' statementExpression)*
	;

forControl
	:	forVarControl
	|	forInit ';' expression? ';' forUpdate?
	;

forInit
	:	'final'? type variableDeclarators
	|	statementExpressionList
	;
	
forVarControl
	:	'final'? (annotation)? type Identifier forVarControlRest
	;

forVarControlRest
	:	variableDeclaratorsRest ';' expression? ';' forUpdate?
	|	':' expression
	;

forUpdate
	:	statementExpressionList
	;

// EXPRESSIONS
parExpression
	:	'(' expression ')'
	;

statementExpressionList
	:	statementExpression (',' statementExpression)*
	;
	
statementExpression
	:	expression
	;

constantExpression
	:	expression
	;
	
expression
	:	conditionalExpression (assignmentOperator expression)?
	;
	
assignmentOperator
	:	'='
	|	'+='
	|	'-='
	|	'*='
	|	'/='
	|	'&amp;='
	|	'|='
	|	'^='
	|	'%='
	|	'&lt;' '&lt;' '='
	|	'&gt;' '&gt;' '='
	|	'&gt;' '&gt;' '&gt;' '='
	;

conditionalExpression
	:	conditionalOrExpression ('?' expression ':' expression)?
	;

conditionalOrExpression
	:	conditionalAndExpression ('||' conditionalAndExpression)*
	;

conditionalAndExpression
	:	inclusiveOrExpression ('&amp;&amp;' inclusiveOrExpression)*
	;

inclusiveOrExpression
	:	exclusiveOrExpression ('|' exclusiveOrExpression)*
	;

exclusiveOrExpression
	:	andExpression ('^' andExpression)*
	;

andExpression
	:	equalityExpression ('&amp;' equalityExpression)*
	;

equalityExpression
	:	instanceOfExpression (('==' | '!=') instanceOfExpression)*
	;

instanceOfExpression
	:	relationalExpression ('instanceof' type)?
	;

relationalExpression
	:	shiftExpression (relationalOp shiftExpression)*
	;
	
relationalOp
	:	('&lt;' '=' | '&gt;' '=' | '&lt;' | '&gt;')
	;

shiftExpression
	:	additiveExpression (shiftOp additiveExpression)*
	;

// TODO: need a sem pred to check column on these &gt;&gt;&gt;
shiftOp
	:	('&lt;' '&lt;' | '&gt;' '&gt;' '&gt;' | '&gt;' '&gt;')
	;

additiveExpression
	:	multiplicativeExpression (('+' | '-') multiplicativeExpression)*
	;

multiplicativeExpression
	:	unaryExpression (('*' | '/' | '%') unaryExpression)*
	;
	
unaryExpression
	:	'+' unaryExpression
	|	'-' unaryExpression
	|	'++' primary
	|	'--' primary
	|	unaryExpressionNotPlusMinus
	;

unaryExpressionNotPlusMinus
	:	'~' unaryExpression
	| 	'!' unaryExpression
	|	castExpression
	|	primary selector* ('++' | '--')?
	;

castExpression
	:	'(' basicType ')' unaryExpression
	|	'(' (expression | type) ')' unaryExpressionNotPlusMinus
	;

primary
	:	parExpression
	|	nonWildcardTypeArguments (explicitGenericInvocationSuffix | 'this' arguments)
	|	'this' (arguments)?
	|	'super' superSuffix
	|	literal
	|	'new' creator
	|	Identifier ('.' Identifier)* (identifierSuffix)?
	|	basicType ('[' ']')* '.' 'class'
	|	'void' '.' 'class'
	;

identifierSuffix
	:	('[' ']')+ '.' 'class'
	|	('[' expression ']')+ // can also be matched by selector, but do here
	|	arguments
	|	'.' 'class'
	|	'.' explicitGenericInvocation
	|	'.' 'this'
	|	'.' 'super' arguments
	|	'.' 'new' (nonWildcardTypeArguments)? innerCreator
	;
	
creator
	:	nonWildcardTypeArguments? createdName
		(arrayCreatorRest | classCreatorRest)
	;

createdName
	:	Identifier nonWildcardTypeArguments?
		('.' Identifier nonWildcardTypeArguments?)*
	|	basicType
	;
	
innerCreator
	:	Identifier classCreatorRest
	;

arrayCreatorRest
	:	'['
	      	(	']' ('[' ']')* arrayInitializer
		|	expression ']' ('[' expression ']')* ('[' ']')*
		)
	;

classCreatorRest
	:	arguments classBody?
	;
	
explicitGenericInvocation
	:	nonWildcardTypeArguments explicitGenericInvocationSuffix
	;
	
nonWildcardTypeArguments
	:	'&lt;' typeList '&gt;'
	;
	
explicitGenericInvocationSuffix
	:	'super' superSuffix
	|	Identifier arguments
	;
	
selector
	:	'.' Identifier (arguments)?
	|	'.' 'this'
	|	'.' 'super' superSuffix
	|	'.' 'new' (nonWildcardTypeArguments)? innerCreator
	|	'[' expression ']'
	;
	
superSuffix
	:	arguments
	|	'.' Identifier (arguments)?
	;

arguments
	:	'(' statementExpressionList? ')'
	;



// LEXER
HexIntegerLiteral
	:	'0' ('x' | 'X') HexDigit+ IntegerTypeSuffix?
	;

DecimalIntegerLiteral
	:	('0' | '1'..'9' '0'..'9'*) IntegerTypeSuffix?
	;

OctalIntegerLiteral
	:	'0' ('0'..'7')+ IntegerTypeSuffix?
	;

fragment
HexDigit
	:	('0'..'9' | 'a'..'f' | 'A'..'F')
	;

fragment
IntegerTypeSuffix
	:	('l' | 'L')
	;

FloatingPointLiteral
	:	('0'..'9')+ '.' ('0'..'9')* Exponent? FloatTypeSuffix?
	|	'.' ('0'..'9')+ Exponent? FloatTypeSuffix?
	|	('0'..'9')+ Exponent FloatTypeSuffix?
	|	('0'..'9')+ Exponent? FloatTypeSuffix
	;

fragment
Exponent
	:	('e' | 'E') ('+' | '-')? ('0'..'9')+
	;

fragment
FloatTypeSuffix
	:	('f' | 'F' | 'd' | 'D')
	;

CharacterLiteral
	:	'\'' (EscapeSequence | ~('\'' | '\\')) '\''
	;

StringLiteral
	:	'&quot;' (EscapeSequence | ~('\\' | '&quot;'))* '&quot;'
	;

fragment
EscapeSequence
	:	'\\' ('b' | 't' | 'n' | 'f' | 'r' | '\&quot;' | '\'' | '\\')
	|	UnicodeEscape
	|	OctalEscape
	;

fragment
OctalEscape
	:	'\\' ('0'..'3') ('0'..'7') ('0'..'7')
	|	'\\' ('0'..'7') ('0'..'7')
	|	'\\' ('0'..'7')
	;

fragment
UnicodeEscape
	:	'\\' 'u' HexDigit HexDigit HexDigit HexDigit
	;

ENUM
	:	'enum' {if ( !enumIsKeyword ) type=Identifier;}
	;
	
Identifier 
	:	JavaLetter (JavaLetter | JavaLetterOrDigit)*
	;

// I found this char range in JavaCC's grammar, but Letter and Digit overlap.
// Still works, but...
fragment
JavaLetter
	:	'\u0024'
	|	'\u0041'..'\u005a'
	|	'\u005f'
	|	'\u0061'..'\u007a'
	|	'\u00c0'..'\u00d6'
	|	'\u00d8'..'\u00f6'
	|	'\u00f8'..'\u00ff'
	|	'\u0100'..'\u1fff'
	|	'\u3040'..'\u318f'
	|	'\u3300'..'\u337f'
	|	'\u3400'..'\u3d2d'
	|	'\u4e00'..'\u9fff'
	|	'\uf900'..'\ufaff'
	;

fragment
JavaLetterOrDigit
	:	'\u0030'..'\u0039'
	|	'\u0660'..'\u0669'
	|	'\u06f0'..'\u06f9'
	|	'\u0966'..'\u096f'
	|	'\u09e6'..'\u09ef'
	|	'\u0a66'..'\u0a6f'
	|	'\u0ae6'..'\u0aef'
	|	'\u0b66'..'\u0b6f'
	|	'\u0be7'..'\u0bef'
	|	'\u0c66'..'\u0c6f'
	|	'\u0ce6'..'\u0cef'
	|	'\u0d66'..'\u0d6f'
	|	'\u0e50'..'\u0e59'
	|	'\u0ed0'..'\u0ed9'
	|	'\u1040'..'\u1049'
	;

WS
	:	(' ' | '\r' | '\t' | '\u000C' | '\n') {channel=99;}
	;

Comment
	:	'/*' (options {greedy=false;} : . )* '*/' {channel=99;}
	;

EndOfLineComment
	:	'//' ~('\n' | '\r')* '\r'? '\n' {channel=99;}
	;
</PRE>




<!--endarticle-->
    <HR>
    <P><UL>
        <!--threads-->
	<LI>Previous message: <A HREF="017476.html">[antlr-interest] v3: conformance of current Java 1.5 grammar
</A></li>
	<LI>Next message: <A HREF="017477.html">[antlr-interest] Exception at compile
</A></li>
         <LI> <B>Messages sorted by:</B> 
              <a href="date.html#17480">[ date ]</a>
              <a href="thread.html#17480">[ thread ]</a>
              <a href="subject.html#17480">[ subject ]</a>
              <a href="author.html#17480">[ author ]</a>
         </LI>
       </UL>

<hr>
<a href="http://www.antlr.org/mailman/listinfo/antlr-interest">More information about the antlr-interest
mailing list</a><br>
</body></html>
