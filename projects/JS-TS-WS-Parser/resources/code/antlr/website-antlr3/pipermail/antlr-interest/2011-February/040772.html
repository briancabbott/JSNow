<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 3.2//EN">
<HTML>
 <HEAD>
   <TITLE> [antlr-interest] Compile errors with CSharp2 Target
   </TITLE>
   <LINK REL="Index" HREF="index.html" >
   <LINK REL="made" HREF="mailto:antlr-interest%40antlr.org?Subject=Re:%20%5Bantlr-interest%5D%20Compile%20errors%20with%20CSharp2%20Target&In-Reply-To=%3CAANLkTind4_5Lpmg3ry_AoMfrq0ue1kZNSEM7PutkbcCb%40mail.gmail.com%3E">
   <META NAME="robots" CONTENT="index,nofollow">
   <META http-equiv="Content-Type" content="text/html; charset=us-ascii">
   <LINK REL="Previous"  HREF="040763.html">
   <LINK REL="Next"  HREF="040780.html">
 </HEAD>
 <BODY BGCOLOR="#ffffff">
   <H1>[antlr-interest] Compile errors with CSharp2 Target</H1>
    <B>Simon</B> 
    <A HREF="mailto:antlr-interest%40antlr.org?Subject=Re:%20%5Bantlr-interest%5D%20Compile%20errors%20with%20CSharp2%20Target&In-Reply-To=%3CAANLkTind4_5Lpmg3ry_AoMfrq0ue1kZNSEM7PutkbcCb%40mail.gmail.com%3E"
       TITLE="[antlr-interest] Compile errors with CSharp2 Target">simonzmail at gmail.com
       </A><BR>
    <I>Tue Feb 15 23:42:19 PST 2011</I>
    <P><UL>
        <LI>Previous message: <A HREF="040763.html">[antlr-interest] Compile errors with CSharp2 Target
</A></li>
        <LI>Next message: <A HREF="040780.html">[antlr-interest] Compile errors with CSharp2 Target
</A></li>
         <LI> <B>Messages sorted by:</B> 
              <a href="date.html#40772">[ date ]</a>
              <a href="thread.html#40772">[ thread ]</a>
              <a href="subject.html#40772">[ subject ]</a>
              <a href="author.html#40772">[ author ]</a>
         </LI>
       </UL>
    <HR>  
<!--beginarticle-->
<PRE>On Wed, Feb 16, 2011 at 10:47 AM, Johannes Luber &lt;<A HREF="http://www.antlr.org/mailman/listinfo/antlr-interest">JALuber at gmx.de</A>&gt; wrote:
&gt;<i> Did you read the other emails stating you may have a grammar which is meant to work with Java? If possible pls attach the grammar to the email, so we can check this and any other errors. I forgot which ANTLR versions require what runtimes exactly, but you need 3.1 or 3.2 for CSharp2. CSharp3 requires ANTLR 3.3 and other assemblies than I've provided, as the targets aren't compatible with each other yet. Not sure if those assemblies are provided in distro or where else you can find them. Sam should know more.
</I>
Ah, thanks to everyone for pointing out the obvious - I looked more
thoroughly in the grammar and in the end did find some stray bits of
Java code which, once eliminated let it compile just fine using
CSharp2.   I still have the same errors with CSharp3 (incorrect escape
sequences when generated with antlr 3.1.3, different errors with antlr
3.3).   I've attached the &quot;purified&quot; Php.g grammar that succeeds in
CSharp2 but fails in CSharp3 in case it is useful - however I think I
will proceed with CSharp2 at this point.

Thanks again for the help!

Simon
-------------- next part --------------
//Todo: Labels and goto have been disabled, need to test on 5.3 

grammar Php;

options {
    backtrack = true; 
    memoize = true;
    k=2;
    output = AST;
    ASTLabelType = CommonTree;
    language = 'CSharp3';
}

tokens{
    SemiColon = ';';
    Comma = ',';
    OpenBrace = '(';
    CloseBrace = ')';
    OpenSquareBrace = '[';
    CloseSquareBrace = ']';
    OpenCurlyBrace = '{';
    CloseCurlyBrace = '}';
    ArrayAssign = '=&gt;';
    LogicalOr = '||';
    LogicalAnd = '&amp;&amp;';
    ClassMember = '::';
    InstanceMember = '-&gt;';
    SuppressWarnings = '@';
    QuestionMark = '?';
    Dollar = '$';
    Colon = ':';
    Dot = '.';
    Ampersand = '&amp;';
    Pipe = '|';
    Bang = '!';
    Plus = '+';
    Minus = '-';
    Asterisk = '*';
    Percent = '%';
    Forwardslash = '/'; 
    Tilde = '~';
    Equals = '=';
    New = 'new';
    Clone = 'clone';
    Echo = 'echo';
    If = 'if';
    Else = 'else';
    ElseIf = 'elseif';
    For = 'for';
    Foreach = 'foreach';
    While = 'while';
    Do = 'do';
    Switch = 'switch';
    Case = 'case';
    Default = 'default';
    Function = 'function';
    Break = 'break';
    Continue = 'continue';
    //Goto = 'goto';
    Return = 'return';
    Global = 'global';
    Static = 'static';
    And = 'and';
    Or = 'or';
    Xor = 'xor';
    Instanceof = 'instanceof';
    
    Class = 'class';
    Interface = 'interface';
    Extends = 'extends';
    Implements = 'implements';
    Abstract = 'abstract';
    Var = 'var';
    Const = 'const';
    Modifiers;
    ClassDefinition;
    
    Block;
    Params;
    Apply;
    Member;
    Reference;
    Empty;
    Prefix;
    Postfix;
    IfExpression;
    Label;
    Cast;
    ForInit;
    ForCondition;
    ForUpdate;
    Field;
    Method;
}

@parser::namespace { PHPParser } 

@lexer::namespace { PHPParser }


@header{
// package com.bnw.php; 
}
@lexer::header{
// package com.bnw.php; 
}
@lexer::members{
}


prog : statement*;

statement
    : simpleStatement? BodyString
    | '{' statement '}' -&gt; statement
    | bracketedBlock
    //| UnquotedString Colon statement -&gt; ^(Label UnquotedString statement)
    | classDefinition
    | interfaceDefinition
    | complexStatement
    | simpleStatement ';'!
    ;
    
bracketedBlock
    : '{' stmts=statement* '}' -&gt; ^(Block $stmts)
    ;

interfaceDefinition
    : Interface interfaceName=UnquotedString interfaceExtends?
        OpenCurlyBrace
        interfaceMember*
        CloseCurlyBrace
        -&gt; ^(Interface $interfaceName interfaceExtends? interfaceMember*)
    ;

interfaceExtends
    : Extends^ UnquotedString (Comma! UnquotedString)*
    ;
interfaceMember
    : Const UnquotedString (Equals atom)? ';' 
        -&gt; ^(Const UnquotedString atom?)
    | fieldModifier* Function UnquotedString parametersDefinition ';'
        -&gt; ^(Method ^(Modifiers fieldModifier*) UnquotedString parametersDefinition)
    ;

classDefinition
    :   classModifier? 
        Class className=UnquotedString 
        (Extends extendsclass=UnquotedString)? 
        classImplements?
        OpenCurlyBrace
        classMember*
        CloseCurlyBrace 
        -&gt; ^(Class ^(Modifiers classModifier?) $className ^(Extends $extendsclass)? classImplements?
            classMember*
        )
    ;
    
classImplements
    :  Implements^ (UnquotedString (Comma! UnquotedString)*)
    ;

classMember
    : fieldModifier* Function UnquotedString parametersDefinition 
        (bracketedBlock | ';')
        -&gt; ^(Method ^(Modifiers fieldModifier*) UnquotedString parametersDefinition bracketedBlock?)
    | Var Dollar UnquotedString (Equals atom)? ';' 
        -&gt; ^(Var ^(Dollar UnquotedString) atom?) 
    | Const UnquotedString (Equals atom)? ';' 
        -&gt; ^(Const UnquotedString atom?)
    | fieldModifier* (Dollar UnquotedString) (Equals atom)? ';' 
        -&gt; ^(Field ^(Modifiers fieldModifier*) ^(Dollar UnquotedString) atom?)
    ;

fieldDefinition
    : Dollar UnquotedString (Equals atom)? ';'-&gt; ^(Field ^(Dollar UnquotedString) atom?)
    ;
    
classModifier
    : 'abstract';
    
fieldModifier
    : AccessModifier | 'abstract' | 'static' 
    ;


complexStatement
    : If '(' ifCondition=expression ')' ifTrue=statement conditional?
        -&gt; ^('if' expression $ifTrue conditional?)
    | For '(' forInit forCondition forUpdate ')' statement -&gt; ^(For forInit forCondition forUpdate statement)
    | Foreach '(' variable 'as' arrayEntry ')' statement -&gt; ^(Foreach variable arrayEntry statement)
    | While '(' whileCondition=expression? ')' statement -&gt; ^(While $whileCondition statement)
    | Do statement While '(' doCondition=expression ')' ';' -&gt; ^(Do statement $doCondition)
    | Switch '(' expression ')' '{'cases'}' -&gt; ^(Switch expression cases)
    | functionDefinition
    ;

simpleStatement
    : Echo^ commaList
    | Global^ name (','! name)*
    | Static^ variable Equals! atom
    | Break^ Integer?
    | Continue^ Integer?
    //| Goto^ UnquotedString
    | Return^ expression?
    | RequireOperator^ expression
    | expression
    ;


conditional
    : ElseIf '(' ifCondition=expression ')' ifTrue=statement conditional? -&gt; ^(If $ifCondition $ifTrue conditional?)
    | Else statement -&gt; statement
    ;

forInit
    : commaList? ';' -&gt; ^(ForInit commaList?)
    ;

forCondition
    : commaList? ';' -&gt; ^(ForCondition commaList?)
    ;
    
forUpdate
    : commaList? -&gt; ^(ForUpdate commaList?)
    ;

cases 
    : casestatement*  defaultcase
    ;

casestatement
    : Case^ expression ':'! statement*
    ;

defaultcase 
    : (Default^ ':'! statement*)
    ;

functionDefinition
    : Function UnquotedString parametersDefinition bracketedBlock -&gt; 
        ^(Function UnquotedString parametersDefinition bracketedBlock)
    ;

parametersDefinition
    : OpenBrace (paramDef (Comma paramDef)*)? CloseBrace -&gt; ^(Params paramDef*) 
    ;

paramDef
    : paramName (Equals^ atom)?
    ;

paramName
    : Dollar^ UnquotedString
    | Ampersand Dollar UnquotedString -&gt; ^(Ampersand ^(Dollar UnquotedString))
    ;

commaList
    : expression (','! expression)* 
    ;
    
expression
    : weakLogicalOr
    ;

weakLogicalOr
    : weakLogicalXor (Or^ weakLogicalXor)*
    ;

weakLogicalXor
    : weakLogicalAnd (Xor^ weakLogicalAnd)*
    ;
    
weakLogicalAnd
    : assignment (And^ assignment)*
    ;

assignment
    : name ((Equals | AsignmentOperator)^ assignment)
    | ternary
    ;

ternary
    : logicalOr QuestionMark expression Colon expression -&gt; ^(IfExpression logicalOr expression*)
    | logicalOr
    ;
    
logicalOr
    : logicalAnd (LogicalOr^ logicalAnd)*
    ;

logicalAnd
    : bitwiseOr (LogicalAnd^ bitwiseOr)*
    ;
    
bitwiseOr
    : bitWiseAnd (Pipe^ bitWiseAnd)*
    ;

bitWiseAnd
    : equalityCheck (Ampersand^ equalityCheck)*
    ;

equalityCheck
    : comparisionCheck (EqualityOperator^ comparisionCheck)?
    ;
    
comparisionCheck
    : bitWiseShift (ComparisionOperator^ bitWiseShift)?
    ;

bitWiseShift
    : addition (ShiftOperator^ addition)*
    ;
    
addition
    : multiplication ((Plus | Minus | Dot)^ multiplication)*
    ;

multiplication
    : logicalNot ((Asterisk | Forwardslash | Percent)^ logicalNot)*
    ;

logicalNot
    : Bang^ logicalNot
    | instanceOf
    ;

instanceOf
    : negateOrCast (Instanceof^ negateOrCast)?
    ;

negateOrCast
    : (Tilde | Minus | SuppressWarnings)^ increment
    | OpenBrace PrimitiveType CloseBrace increment -&gt; ^(Cast PrimitiveType increment)
    | OpenBrace! weakLogicalAnd CloseBrace!
    | increment
    ;

increment
    : IncrementOperator name -&gt; ^(Prefix IncrementOperator name)
    | name IncrementOperator -&gt; ^(Postfix IncrementOperator name)
    | newOrClone
    ;

newOrClone
    : New^ nameOrFunctionCall
    | Clone^ name
    | atomOrReference
    ;

atomOrReference
    : atom
    | reference
    ;

arrayDeclaration
    : Array OpenBrace (arrayEntry (Comma arrayEntry)*)? CloseBrace -&gt; ^(Array arrayEntry*)
    ;

arrayEntry
    : (keyValuePair | expression)
    ;

keyValuePair
    : (expression ArrayAssign expression) -&gt; ^(ArrayAssign expression+)
    ;

atom: SingleQuotedString | DoubleQuotedString | Integer | Real | Boolean | arrayDeclaration
    ;

//Need to be smarter with references, they have their own tower of application.
reference
    : Ampersand^ nameOrFunctionCall
    | nameOrFunctionCall
    ;

nameOrFunctionCall
    : name OpenBrace (expression (Comma expression)*)? CloseBrace -&gt; ^(Apply name expression*)
    | name
    ;

name: staticMemberAccess
    | memberAccess
    | variable
    ;
    
staticMemberAccess
    : UnquotedString '::'^ variable
    ;

memberAccess
    : variable 
        ( OpenSquareBrace^ expression CloseSquareBrace!
        | '-&gt;'^ UnquotedString)*
    ;
    
variable
    : Dollar^ variable
    | UnquotedString
    ;

BodyString 
    : '?&gt;' (('&lt;' ~ '?')=&gt; '&lt;' | ~'&lt;' )* ('&lt;?' ('php'?))?
    ;

fragment
FirstBodyString
    : (('&lt;' ~ '?')=&gt; '&lt;' | ~'&lt;' )* '&lt;?' ('php'?)
    ;

MultilineComment    
    : '/*' (('*' ~ '/')=&gt;'*' | ~ '*')* '*/' {$channel=HIDDEN;}
    ;

SinglelineComment
    : '//'  (('?' ~'&gt;')=&gt;'?' | ~('\n'|'?'))* {$channel=HIDDEN;}
    ;

UnixComment
    : '#' (('?' ~'&gt;')=&gt;'?' | ~('\n'|'?'))* {$channel=HIDDEN;}
    ;
    

Array
    : ('a'|'A')('r'|'R')('r'|'R')('a'|'A')('y'|'Y')
    ;

RequireOperator
    : 'require' | 'require_once' | 'include' | 'include_once'
    ;

PrimitiveType
    : 'int'|'float'|'string'|'array'|'object'|'bool'
    ;

AccessModifier
    : 'public' | 'private' | 'protected' 
    ;

fragment
Decimal	
	:('1'..'9' ('0'..'9')*)|'0'
	;
fragment
Hexadecimal
	: '0'('x'|'X')('0'..'9'|'a'..'f'|'A'..'F')+
	;
	
fragment
Octal
	: '0'('0'..'7')+
	;
Integer
	:Octal|Decimal|Hexadecimal
	;
	
fragment
Digits
	: '0'..'9'+
	;
	
fragment
DNum
	:(('.' Digits)=&gt;('.' Digits)|(Digits '.' Digits?))
	;
	
fragment
Exponent_DNum
	:((Digits|DNum)('e'|'E')('+''-')?Digits)
	;
	
Real
    : DNum|Exponent_DNum
    ;

Boolean
    : 'true' | 'false'
    ;

SingleQuotedString
    : '\'' (('\\' '\'')=&gt;'\\' '\''
    |         ('\\' '\\')=&gt;'\\' '\\' 
    |         '\\' | ~ ('\'' | '\\'))* 
      '\''
    ;

fragment
EscapeCharector
    : 'n' | 'r' | 't' | '\\' | '$' | '&quot;' | Digits | 'x'
    ;

DoubleQuotedString
    : '&quot;'  ( ('\\' EscapeCharector)=&gt; '\\' EscapeCharector 
    | '\\' 
    | ~('\\'|'&quot;') )* 
      '&quot;'
    ;

//Todo handle '\x7f' - '\xff'
UnquotedString
   : ('a'..'z' | 'A'..'Z' | '_')  ('a'..'z' | 'A'..'Z' | '0'..'9' | '_')*
   ;
   
AsignmentOperator
    : '+='|'-='|'*='|'/='|'.='|'%='|'&amp;='|'|='|'^='|'&lt;&lt;='|'&gt;&gt;='
    ;
    
EqualityOperator
    : '==' | '!=' | '===' | '!=='
    ;

ComparisionOperator
    : '&lt;' | '&lt;=' | '&gt;' | '&gt;=' | '&lt;&gt;'
    ;
    
ShiftOperator
    : '&lt;&lt;' | '&gt;&gt;'
    ;

IncrementOperator
    : '--'|'++'
    ;
    

fragment
Eol : '\n'
    ;

WhiteSpace
@init{
    $channel=HIDDEN;
}
	:	(' '| '\t'| '\n'|'\r')*
	;
	


</PRE>







<!--endarticle-->
    <HR>
    <P><UL>
        <!--threads-->
	<LI>Previous message: <A HREF="040763.html">[antlr-interest] Compile errors with CSharp2 Target
</A></li>
	<LI>Next message: <A HREF="040780.html">[antlr-interest] Compile errors with CSharp2 Target
</A></li>
         <LI> <B>Messages sorted by:</B> 
              <a href="date.html#40772">[ date ]</a>
              <a href="thread.html#40772">[ thread ]</a>
              <a href="subject.html#40772">[ subject ]</a>
              <a href="author.html#40772">[ author ]</a>
         </LI>
       </UL>

<hr>
<a href="http://www.antlr.org/mailman/listinfo/antlr-interest">More information about the antlr-interest
mailing list</a><br>
</body></html>
