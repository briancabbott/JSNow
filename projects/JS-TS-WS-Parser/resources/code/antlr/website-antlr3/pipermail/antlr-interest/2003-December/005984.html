<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 3.2//EN">
<HTML>
 <HEAD>
   <TITLE> [antlr-interest] IDL3 Grammar
   </TITLE>
   <LINK REL="Index" HREF="index.html" >
   <LINK REL="made" HREF="mailto:antlr-interest%40antlr.org?Subject=%5Bantlr-interest%5D%20IDL3%20Grammar&In-Reply-To=">
   <META NAME="robots" CONTENT="index,nofollow">
   <META http-equiv="Content-Type" content="text/html; charset=us-ascii">
   <LINK REL="Previous"  HREF="005983.html">
   <LINK REL="Next"  HREF="005985.html">
 </HEAD>
 <BODY BGCOLOR="#ffffff">
   <H1>[antlr-interest] IDL3 Grammar</H1>
    <B>Gary Duzan</B> 
    <A HREF="mailto:antlr-interest%40antlr.org?Subject=%5Bantlr-interest%5D%20IDL3%20Grammar&In-Reply-To="
       TITLE="[antlr-interest] IDL3 Grammar">gduzan at bbn.com
       </A><BR>
    <I>Wed Dec 31 08:19:12 PST 2003</I>
    <P><UL>
        <LI>Previous message: <A HREF="005983.html">New antlr-interest member
</A></li>
        <LI>Next message: <A HREF="005985.html">[antlr-interest] IDL3 Grammar
</A></li>
         <LI> <B>Messages sorted by:</B> 
              <a href="date.html#5984">[ date ]</a>
              <a href="thread.html#5984">[ thread ]</a>
              <a href="subject.html#5984">[ subject ]</a>
              <a href="author.html#5984">[ author ]</a>
         </LI>
       </UL>
    <HR>  
<!--beginarticle-->
<PRE>   On the theory that I many need to parse some modern IDL in the near
future, I've been hacking on Jim Coker's IDL grammar to add CORBA 3.x
features and AST creation. I have something that seems to be working
reasonably well and would be interested in any comments. I'll try
attaching it to this message, but if Yahoo decides to eat the attachment
I can put it up on the web.

					Gary Duzan
					BBN Technologies
					A Verizon Company



 

Yahoo! Groups Links

To visit your group on the web, go to:
 <A HREF="http://groups.yahoo.com/group/antlr-interest/">http://groups.yahoo.com/group/antlr-interest/</A>

To unsubscribe from this group, send an email to:
 <A HREF="http://www.antlr.org/mailman/listinfo/antlr-interest">antlr-interest-unsubscribe at yahoogroups.com</A>

Your use of Yahoo! Groups is subject to:
 <A HREF="http://docs.yahoo.com/info/terms/">http://docs.yahoo.com/info/terms/</A> 

-------------- next part --------------
import java.io.*;
import antlr.*;

public class Main {
  public static void main(String[] args) {
    try {
      IDLLexer lexer = new IDLLexer(new DataInputStream(System.in));
      IDLParser parser = new IDLParser(lexer);
      parser.specification();
      System.out.println(parser.getAST().toStringTree());
    } catch(Exception e) {
      System.err.println(&quot;exception: &quot;+e);
    }
  }
}

-------------- next part --------------
header {
  import java.io.*;
  import java.util.Vector;
  import java.util.Hashtable;
 }

/**
 *  This is a complete parser for the IDL language as defined
 *  by the CORBA 3.0.2 specification.  It will allow those who
 *  need an IDL parser to get up-and-running very quickly.
 *  Though IDL's syntax is very similar to C++, it is also
 *  much simpler, due in large part to the fact that it is
 *  a declarative-only language.
 *
 *  Some things that are not included are: Symbol table construction
 *  (it is not necessary for parsing, btw) and preprocessing (for
 *  IDL compiler #pragma directives). You can use just about any
 *  C or C++ preprocessor, but there is an interesting semantic
 *  issue if you are going to generate code: In C, #include is
 *  a literal include, in IDL, #include is more like Java's import:
 *  It adds definitions to the scope of the parse, but included
 *  definitions are not generated.
 *
 *  Jim Coker, <A HREF="http://www.antlr.org/mailman/listinfo/antlr-interest">jcoker at magelang.com</A>
 *  Gary Duzan, <A HREF="http://www.antlr.org/mailman/listinfo/antlr-interest">gduzan at bbn.com</A>
 */
class IDLParser extends Parser;
options {
	exportVocab=IDL;
	buildAST=true;
	k=4;
}

specification
	:   (import_dcl)* (definition)+
	;


definition
	:   (   type_dcl SEMI!
	    |   const_dcl SEMI!
	    |   except_dcl SEMI!
	    |   ((&quot;abstract&quot; | &quot;local&quot;)? &quot;interface&quot;) =&gt; interf SEMI!
	    |   module SEMI!
	    |   ((&quot;abstract&quot; | &quot;custom&quot;)? &quot;valuetype&quot;) =&gt; value SEMI!
	    |   type_id_dcl SEMI!
	    |   type_prefix_dcl SEMI!
	    |   ((&quot;abstract&quot; | &quot;custom&quot;)? &quot;eventtype&quot;) =&gt; event SEMI!
	    |   component SEMI!
	    |   home_dcl SEMI!
	    )
	;

module
	:    &quot;module&quot;^
	     identifier
	     LCURLY! d:definition_list RCURLY!
	;

definition_list
	:   (definition)+
	;

interf
	:   ( &quot;abstract&quot; | &quot;local&quot; )?
	    &quot;interface&quot;^
	    identifier
	    // interface_name_dcl
	    (   interface_dcl
	    |   // forward_dcl
	    )
	;

interface_dcl
	:   interface_header
	    LCURLY! interface_body RCURLY!
	;

// forward_dcl
// 	:   // interface_name_dcl
// 	;

// interface_name_dcl
// 	:   ( &quot;abstract&quot; | &quot;local&quot; )?
// 	    &quot;interface&quot;^
// 	    identifier
// 	;

interface_header
	:   // interface_name_dcl
	    ( interface_inheritance_spec )?
	;

interface_body
	:   ( export )*
	;

export
	:   (   type_dcl SEMI!
	    |   const_dcl SEMI!
	    |   except_dcl SEMI!
	    |   attr_dcl SEMI!
	    |   op_dcl SEMI!
	    |   type_id_dcl SEMI!
	    |   type_prefix_dcl SEMI!
	    )
	;


interface_inheritance_spec
	:   COLON^ scoped_name_list
	;

interface_name
	:   scoped_name
	;

scoped_name_list
	:    scoped_name (COMMA! scoped_name)*
	;


scoped_name
	:   ( SCOPEOP^ )? IDENT^ /* identifier */ (SCOPEOP! identifier)*
	;

value
	:   ( value_dcl
	    | value_abs_dcl
	    | value_box_dcl
	    | value_custom_dcl
	    | value_forward_dcl
	    )
	;

value_forward_dcl
	:   &quot;valuetype&quot;^
	    identifier
	;

value_box_dcl
	:   &quot;valuetype&quot;^
	    identifier
	    type_spec
	;

value_abs_dcl
	:   &quot;abstract&quot;
	    &quot;valuetype&quot;^
	    identifier
	    ( value_abs_full_dcl
	    | // value_abs_forward_dcl
	    )
	;

value_abs_full_dcl
	:   value_inheritance_spec
	    LCURLY! ( export )* RCURLY!
	;

// value_abs_forward_dcl
// 	:
// 	;

value_dcl
	:   value_header
	    LCURLY! ( value_element )* RCURLY!
	;

value_custom_dcl
	:   &quot;custom&quot;^
	    value_dcl
	;

value_header
	:   &quot;valuetype&quot;^
	    identifier
	    value_inheritance_spec
	;

value_inheritance_spec
/*
	:   ( COLON ( &quot;truncatable&quot; )?
	      value_name ( COMMA! value_name )*
	    )?
	    ( &quot;supports&quot; interface_name ( COMMA! interface_name )* )?
	;
*/
	:   ( value_value_inheritance_spec )?
	    ( value_interface_inheritance_spec )?
	;

value_value_inheritance_spec
	:   COLON^ ( &quot;truncatable&quot; )?
	    value_name ( COMMA! value_name )*
	;

value_interface_inheritance_spec
	:   &quot;supports&quot;^ interface_name ( COMMA! interface_name )*
	;

value_name
	:   scoped_name
	;

value_element
	:   ( export
	    | state_member
	    | init_dcl
	    )
	;

state_member
	:   ( &quot;public&quot; | &quot;private&quot; )
	    type_spec declarators SEMI!
	;

init_dcl
	:   &quot;factory&quot;^ identifier
	    LPAREN! (init_param_decls)? RPAREN!
	    (raises_expr)?
	    SEMI!
	;

init_param_decls
	:   init_param_decl ( COMMA! init_param_decl )*
	;

init_param_decl
	:   init_param_attribute
	    param_type_spec
	    simple_declarator
	;

init_param_attribute
	:   &quot;in&quot;
	;

const_dcl
	:   &quot;const&quot;^ const_type identifier ASSIGN! const_exp
	;

const_type
	:   (integer_type) =&gt; integer_type
	|   char_type
	|   wide_char_type
	|   boolean_type
	|   floating_pt_type
	|   string_type
	|   wide_string_type
	|   fixed_pt_const_type
	|   scoped_name
	|   octet_type
	;


/*   EXPRESSIONS   */

const_exp
	:   or_expr
	;

or_expr
	:   xor_expr
	    ( OR^ // or_op
	      xor_expr
	    )*
	;

// or_op
// 	:    OR
// 	;


xor_expr
	:   and_expr
	    ( XOR^ // xor_op
	      and_expr
	    )*
	;

// xor_op
// 	:    XOR
// 	;

and_expr
	:   shift_expr
	    ( AND^ // and_op
	      shift_expr
	    )*
	;

// and_op
// 	:    AND
// 	;


shift_expr
	:   add_expr
	    ( ( LSHIFT^
	      | RSHIFT^
	      ) // shift_op
	    add_expr
	    )*
	;

// shift_op
// 	:    LSHIFT
// 	|    RSHIFT
// 	;


add_expr
	:   mult_expr
	    ( ( PLUS^
	      | MINUS^
	      ) // add_op
	      mult_expr
	    )*
	;

// add_op
// 	:    PLUS
// 	|    MINUS
// 	;

mult_expr
	:   unary_expr
	    ( ( STAR^
	      | DIV^
	      | MOD^
	      ) // mult_op
	      unary_expr
	    )*
	;

// mult_op
// 	:    STAR
// 	|    DIV
// 	|    MOD
// 	;

unary_expr
	:   ( MINUS^
	    | PLUS^
	    | TILDE^
	    ) // unary_operator
	    primary_expr
	|   primary_expr
	;

// unary_operator
// 	:   MINUS
// 	|   PLUS
// 	|   TILDE
// 	;

// Node of type TPrimaryExp serves to avoid inf. recursion on tree parse
primary_expr
	:   scoped_name
	|   literal
	|   LPAREN^ const_exp RPAREN
	;

literal
	:   integer_literal
	|   string_literal
	|   wide_string_literal
	|   character_literal
	|   wide_character_literal
	|   fixed_pt_literal
	|   floating_pt_literal
	|   boolean_literal
	;

boolean_literal
	:   &quot;TRUE&quot;
	|   &quot;FALSE&quot;
	;

positive_int_const
	:    const_exp
	;


type_dcl
	:   &quot;typedef&quot;^ type_declarator
	|   (struct_type) =&gt; struct_type
	|   (union_type) =&gt; union_type
	|   enum_type
	|   &quot;native&quot;^ simple_declarator
	|   constr_forward_decl
	;

type_declarator
	:   type_spec declarators
	;

type_spec
	:   simple_type_spec
	|   constr_type_spec
	;

simple_type_spec
	:   base_type_spec
	|   template_type_spec
	|   scoped_name
	;

base_type_spec
	:   (floating_pt_type) =&gt; floating_pt_type	
	|   integer_type	
	|   char_type		
	|   wide_char_type		
	|   boolean_type	
	|   octet_type
	|   any_type
	|   object_type
	|   value_base_type
	;

template_type_spec
	:   sequence_type
	|   string_type
	|   wide_string_type
	|   fixed_pt_type
	;

constr_type_spec
	:   struct_type
	|   union_type
	|   enum_type
	;

declarators
	:   declarator (COMMA! declarator)*
	;

declarator
	:   simple_declarator
	|   complex_declarator
	;

simple_declarator
	:   identifier
	;

complex_declarator
	:   array_declarator
	;

floating_pt_type
	:   &quot;float&quot;
	|   &quot;double&quot;
	|   &quot;long&quot;^ &quot;double&quot;
	;

integer_type
	:  signed_int
	|  unsigned_int
	;

signed_int
	:  signed_short_int
	|  signed_long_int
	|  signed_longlong_int
	;

signed_short_int
	:  &quot;short&quot;
	;

signed_long_int
	:  &quot;long&quot;
	;

signed_longlong_int
	:  &quot;long&quot; &quot;long&quot;
	;

unsigned_int
	:  unsigned_short_int
	|  unsigned_long_int
	|  unsigned_longlong_int
	;

unsigned_short_int
	:  &quot;unsigned&quot; &quot;short&quot;
	;

unsigned_long_int
	:  &quot;unsigned&quot; &quot;long&quot;
	;

unsigned_longlong_int
	:  &quot;unsigned&quot; &quot;long&quot; &quot;long&quot;
	;

char_type
	:   &quot;char&quot;
	;

wide_char_type
	:   &quot;wchar&quot;
	;

boolean_type
	:   &quot;boolean&quot;
	;

octet_type
	:   &quot;octet&quot;
	;

any_type
	:   &quot;any&quot;
	;

object_type
	:   &quot;Object&quot;
	;

struct_type
	:   &quot;struct&quot;^
	    identifier
	    LCURLY! member_list RCURLY!
	;

member_list
	:   (member)+
	;

member
	:   type_spec declarators SEMI!
	;

union_type
	:   &quot;union&quot;^
	    identifier
	    &quot;switch&quot;! LPAREN! switch_type_spec RPAREN!
	    LCURLY! switch_body RCURLY!
	;

switch_type_spec
	:   integer_type
	|   char_type
	|   boolean_type
	|   enum_type
	|   scoped_name
	;

switch_body
	:   case_stmt_list
	;

case_stmt_list
	:  (case_stmt)+
	;

case_stmt
	:   // case_label_list
	    ( &quot;case&quot;^ const_exp COLON!
	    | &quot;default&quot;^ COLON!
	    )+
	    element_spec SEMI!
	;

// case_label_list
// 	:   (case_label)+
// 	;


// case_label
// 	:   &quot;case&quot;^ const_exp COLON!
// 	|   &quot;default&quot;^ COLON!
// 	;

element_spec
	:   type_spec declarator
	;

enum_type
	:   &quot;enum&quot;^ identifier LCURLY! enumerator_list RCURLY!
	;

enumerator_list
	:    enumerator (COMMA! enumerator)*
	;

enumerator
	:   identifier
	;

sequence_type
	:   &quot;sequence&quot;^
	     LT! simple_type_spec opt_pos_int GT!
	;

opt_pos_int
	:    (COMMA! positive_int_const)?
	;

string_type
	:   &quot;string&quot;^ (LT! positive_int_const GT!)?
	;

wide_string_type
	:   &quot;wstring&quot;^ (LT! positive_int_const GT!)?
	;

array_declarator
	:   IDENT^					// identifier
	    (fixed_array_size)+
	;

fixed_array_size
	:   LBRACK! positive_int_const RBRACK!
	;

attr_dcl
	:   readonly_attr_spec
	|   attr_spec
	;

except_dcl
	:   &quot;exception&quot;^
	    identifier
	    LCURLY! opt_member_list RCURLY!
	;


opt_member_list
	:    (member)*
	;

op_dcl
	:   (op_attribute)?
	    op_type_spec
	    IDENT^				// identifier
	    parameter_dcls
	    (raises_expr)?
	    (context_expr)?
	;

op_attribute
	:   &quot;oneway&quot;
	;

op_type_spec
	:   param_type_spec
	|   &quot;void&quot;
	;

parameter_dcls
	:   LPAREN! (param_dcl_list)? RPAREN!
	;

param_dcl_list
	:   param_dcl (COMMA! param_dcl)*
	;

param_dcl
	:   (&quot;in&quot;^ | &quot;out&quot;^ | &quot;inout&quot;^)		// param_attribute
	    param_type_spec simple_declarator
	;

// param_attribute
// 	:   &quot;in&quot;
// 	|   &quot;out&quot;
// 	|   &quot;inout&quot;
// 	;

raises_expr
	:   &quot;raises&quot;^ LPAREN! scoped_name_list RPAREN!
	;

context_expr
	:   &quot;context&quot;^ LPAREN! string_literal_list RPAREN!
	;

string_literal_list
	:    string_literal (COMMA! string_literal)*
	;

param_type_spec
	:   base_type_spec
	|   string_type
	|   wide_string_type
	|   scoped_name
	;

fixed_pt_type
	:   &quot;fixed&quot;^ LT! positive_int_const COMMA! positive_int_const GT!
	;

fixed_pt_const_type
	:   &quot;fixed&quot;
	;

value_base_type
	:   &quot;ValueBase&quot;
	;

constr_forward_decl
	:   &quot;struct&quot;^ identifier
	|   &quot;union&quot;^ identifier
	;

import_dcl
	:   &quot;import&quot;^ imported_scope SEMI!
	;

imported_scope
	:   scoped_name
	|   string_literal
	;

type_id_dcl
	:   &quot;typeid&quot;^
	    scoped_name
	    string_literal
	;

type_prefix_dcl
	:   &quot;typeprefix&quot;^
	    scoped_name
	    string_literal
	;

readonly_attr_spec
	:   &quot;readonly&quot; &quot;attribute&quot;^
	    param_type_spec
	    readonly_attr_declarator
	;

readonly_attr_declarator
	:   simple_declarator
	    ( raises_expr
	    | (COMMA! simple_declarator)*
	    )
	;

attr_spec
	:   &quot;attribute&quot;^ param_type_spec attr_declarator
	;

attr_declarator
	:   simple_declarator
	    ( (&quot;getraises&quot; | &quot;setraises&quot;) =&gt; attr_raises_expr
	    | (COMMA! simple_declarator)*
	    )
	;

attr_raises_expr
	:   (get_excep_expr)?
	    (set_excep_expr)?
	;

get_excep_expr
	:   &quot;getraises&quot;^ exception_list
	;

set_excep_expr
	:   &quot;setraises&quot;^ exception_list
	;

exception_list
	:   LPAREN! scoped_name (COMMA! scoped_name)* RPAREN!
	;

// Component Stuff

component
	:   &quot;component&quot;^
	    identifier
	    (component_dcl)?
	;

component_dcl
	:   (component_inheritance_spec)?
	    (supported_interface_spec)?
	    LCURLY! component_body RCURLY!
	;

supported_interface_spec
	:   &quot;supports&quot;^ scoped_name ( COMMA! scoped_name )*
	;

component_inheritance_spec
	:   COLON^ scoped_name
	;

component_body
	:   (component_export)*
	;

component_export
	:   ( provides_dcl SEMI!
	    | uses_dcl SEMI!
	    | emits_dcl SEMI!
	    | publishes_dcl SEMI!
	    | consumes_dcl SEMI!
	    | attr_dcl SEMI!
	    )
	;

provides_dcl
	:   &quot;provides&quot;^ interface_type identifier
	;

interface_type
	:   ( scoped_name
	    | &quot;Object&quot;
	    )
	;

uses_dcl
	:   &quot;uses&quot;^ (&quot;multiple&quot;)? interface_type identifier
	;

emits_dcl
	:   &quot;emits&quot;^ scoped_name identifier
	;

publishes_dcl
	:   &quot;publishes&quot;^ scoped_name identifier
	;

consumes_dcl
	:   &quot;consumes&quot;^ scoped_name identifier
	;

home_dcl
	:   home_header home_body
	;

home_header
	:   &quot;home&quot;^ identifier
	    (home_inheritance_spec)?
	    (supported_interface_spec)?
	    &quot;manages&quot;! scoped_name
	    (primary_key_spec)?
	;

home_inheritance_spec
	:   COLON^ scoped_name
	;

primary_key_spec
	:   &quot;primarykey&quot;^ scoped_name
	;

home_body
	:   LCURLY! (home_export)* RCURLY!
	;

home_export
	:   ( export
	    | factory_dcl SEMI!
	    | finder_dcl SEMI!
	    )
	;

factory_dcl
	:   &quot;factory&quot;^ identifier
	    LPAREN! init_param_decls RPAREN!
	    (raises_expr)?
	;

finder_dcl
	:   &quot;finder&quot;^ identifier
	    LPAREN! init_param_decls RPAREN!
	    (raises_expr)?
	;

event
	:   ( event_abs
	    | event_custom
	    | event_dcl
	    )
	;

event_header
	:   &quot;eventtype&quot;^
	    identifier
	;

event_abs
	:   &quot;abstract&quot;^
	    event_header
	    (event_abs_dcl)?
	;

event_abs_dcl
	:   value_inheritance_spec
	    LCURLY! (export)* RCURLY!
	;

event_custom
	:   &quot;custom&quot;^
	    event_header
	    event_elem_dcl
	;

event_dcl
	:   event_header
	    ( event_elem_dcl
	    | // event_forward_dcl
	    )
	;

event_elem_dcl
	:   value_inheritance_spec
	    LCURLY! (export)* RCURLY!
	;

// event_forward_dcl
// 	:
// 	;

/* literals */
integer_literal
	:   INT
	|   OCTAL
	|   HEX
	;

string_literal
	:  (STRING_LITERAL)+
	;

wide_string_literal
	:  (WIDE_STRING_LITERAL)+
	;

character_literal
	:  CHAR_LITERAL
	;

wide_character_literal
	:  WIDE_CHAR_LITERAL
	;

fixed_pt_literal
	:  FIXED
	;

floating_pt_literal
	:   f:FLOAT
	;

identifier
	:   IDENT
	;

/* IDL LEXICAL RULES  */
class IDLLexer extends Lexer;
options {
	exportVocab=IDL;
	charVocabulary='\u0000'..'\uFFFE';
	k=4;
}

SEMI
options {
  paraphrase = &quot;;&quot;;
}
	:	';'
	;

QUESTION
options {
  paraphrase = &quot;?&quot;;
}
	:	'?'
	;

LPAREN
options {
  paraphrase = &quot;(&quot;;
}
	:	'('
	;

RPAREN
options {
  paraphrase = &quot;)&quot;;
}
	:	')'
	;

LBRACK
options {
  paraphrase = &quot;[&quot;;
}
	:	'['
	;

RBRACK
options {
  paraphrase = &quot;]&quot;;
}
	:	']'
	;

LCURLY
options {
  paraphrase = &quot;{&quot;;
}
	:	'{'
	;

RCURLY
options {
  paraphrase = &quot;}&quot;;
}
	:	'}'
	;

OR
options {
  paraphrase = &quot;|&quot;;
}
	:	'|'
	;

XOR
options {
  paraphrase = &quot;^&quot;;
}
	:	'^'
	;

AND
options {
  paraphrase = &quot;&amp;&quot;;
}
	:	'&amp;'
	;

COLON
options {
  paraphrase = &quot;:&quot;;
}
	:	':'
	;

COMMA
options {
  paraphrase = &quot;,&quot;;
}
	:	','
	;

DOT
options {
  paraphrase = &quot;.&quot;;
}
	:	'.'
	;

ASSIGN
options {
  paraphrase = &quot;=&quot;;
}
	:	'='
	;

NOT
options {
  paraphrase = &quot;!&quot;;
}
	:	'!'
	;

LT
options {
  paraphrase = &quot;&lt;&quot;;
}
	:	'&lt;'
	;

LSHIFT
options {
  paraphrase = &quot;&lt;&lt;&quot;;
}
	: &quot;&lt;&lt;&quot;
	;

GT
options {
  paraphrase = &quot;&gt;&quot;;
}
	:	'&gt;'
	;

RSHIFT
options {
  paraphrase = &quot;&gt;&gt;&quot;;
}
	: &quot;&gt;&gt;&quot;
	;

DIV
options {
  paraphrase = &quot;/&quot;;
}
	:	'/'
	;

PLUS
options {
  paraphrase = &quot;+&quot;;
}
	:	'+'
	;

MINUS
options {
  paraphrase = &quot;-&quot;;
}
	:	'-'
	;

TILDE
options {
  paraphrase = &quot;~&quot;;
}
	:	'~'
	;

STAR
options {
  paraphrase = &quot;*&quot;;
}
	:	'*'
	;

MOD
options {
  paraphrase = &quot;%&quot;;
}
	:	'%'
	;


SCOPEOP
options {
  paraphrase = &quot;::&quot;;
}
	:  	&quot;::&quot;
	;

WS
options {
  paraphrase = &quot;white space&quot;;
}
	:	(' '
	|	'\t'
	|	'\n'  { newline(); }
	|	'\r')
		{ $setType(Token.SKIP); }
	;


PREPROC_DIRECTIVE
options {
  paraphrase = &quot;a preprocessor directive&quot;;
}

	:
	'#'!
	(~'\n')* '\n'!
	{ $setType(Token.SKIP); newline(); }
	;


SL_COMMENT
options {
  paraphrase = &quot;a comment&quot;;
}

	:
	&quot;//&quot;!
	(~'\n')* '\n'
	{ $setType(Token.SKIP); newline(); }
	;

ML_COMMENT
options {
  paraphrase = &quot;a comment&quot;;
}
	:
	&quot;/*&quot;!
	(
			'\n' { newline(); }
		|	('*')+
			(	'\n' { newline(); }
			|	~('*' | '/' | '\n')
			)
		|	~('*' | '\n')
	)*
	&quot;*/&quot;!
	{ $setType(Token.SKIP);  }
	;

CHAR_LITERAL
options {
  paraphrase = &quot;a character literal&quot;;
}
	:
	'\''!
	( ESC | ~'\'' )
	'\''!
	;

WIDE_CHAR_LITERAL
options {
  paraphrase = &quot;a wide character literal&quot;;
}
	: 'L'! CHAR_LITERAL
	;

STRING_LITERAL
options {
  paraphrase = &quot;a string literal&quot;;
}
	:
	'&quot;'!
	(ESC|~'&quot;')*
	'&quot;'!
	;


WIDE_STRING_LITERAL
options {
  paraphrase = &quot;a wide string literal&quot;;
}
	:
	'L'! STRING_LITERAL
	;

protected
ESC
options {
  paraphrase = &quot;an escape sequence&quot;;
}
	:	'\\'!
		(	'n'		{$setText(&quot;\n&quot;);}
		|	't'		{$setText(&quot;\t&quot;);}
		|	'v'		{$setText(&quot;\013&quot;);}
		|	'b'		{$setText(&quot;\b&quot;);}
		|	'r'		{$setText(&quot;\r&quot;);}
		|	'f'		{$setText(&quot;\r&quot;);}
		|	'a'  		{$setText(&quot;\007&quot;);}
		|	'\\'		{$setText(&quot;\\&quot;);}
		|	'?'     	{$setText(&quot;?&quot;);}
		|	'\''		{$setText(&quot;'&quot;);}
		|	'&quot;'		{$setText(&quot;\&quot;&quot;);}
		|	OCTDIGIT
			(options {greedy=true;}:OCTDIGIT
			  (options {greedy=true;}:OCTDIGIT)?
			)?
			{char realc = (char) Integer.valueOf($getText, 8).intValue(); $setText(realc);}
		|       'x'! HEXDIGIT
			(options {greedy=true;}:HEXDIGIT)?
			{char realc = (char) Integer.valueOf($getText, 16).intValue(); $setText(realc);}
		|	'u'!
			HEXDIGIT
			(options {greedy=true;}:HEXDIGIT
			  (options {greedy=true;}:HEXDIGIT
			    (options {greedy=true;}:HEXDIGIT)?
			  )?
			)?
			{char realc = (char) Integer.valueOf($getText, 16).intValue(); $setText(realc);}
		)
	;

protected
VOCAB
options {
  paraphrase = &quot;an escaped character value&quot;;
}
	:	'\3'..'\377'
	;

protected
DIGIT
options {
  paraphrase = &quot;a digit&quot;;
}
	:	'0'..'9'
	;

protected
NONZERODIGIT
options {
  paraphrase = &quot;a non-zero digit&quot;;
}
	:	'1'..'9'
	;

protected
OCTDIGIT
options {
  paraphrase = &quot;an octal digit&quot;;
}
	:	'0'..'7'
	;

protected
HEXDIGIT
options {
  paraphrase = &quot;a hexadecimal digit&quot;;
}
	:	('0'..'9' | 'a'..'f' | 'A'..'F')
	;

HEX
options {
  paraphrase = &quot;a hexadecimal value value&quot;;
}

	:    (&quot;0x&quot; | &quot;0X&quot;) (HEXDIGIT)+
	;

INT
options {
  paraphrase = &quot;an integer value&quot;;
}
	:    NONZERODIGIT (DIGIT)*                  // base-10
	     (  '.' (DIGIT)*
		 ( (('e' | 'E') ('+' | '-')? (DIGIT)+)	{$setType(FLOAT);}
		 | ('d' | 'D')!				{$setType(FIXED);}
		 |					{$setType(FLOAT);}
		 )
	     |   ('e' | 'E') ('+' | '-')? (DIGIT)+   	{$setType(FLOAT);}
	     |   ('d' | 'D')!				{$setType(FIXED);}
	     )?
	;

OCTAL
options {
  paraphrase = &quot;an octal value&quot;;
}
	:    '0'
	     ( (DIGIT)+
	     | FLOAT					{$setType(FLOAT);}
	     | ('d' | 'D')!				{$setType(FIXED);}
	     |						{$setType(INT);}
	     )
	;


FLOAT
options {
  paraphrase = &quot;a floating point value&quot;;
}

	:    '.' (DIGIT)+
	     ( ('e' | 'E') ('+' | '-')? (DIGIT)+
	     | ('d' | 'D')!				{$setType(FIXED);}
	     )?
	;

IDENT
options {
  paraphrase = &quot;an identifer&quot;;
  testLiterals = true;
}

	:   ('a'..'z'|'A'..'Z') ('a'..'z'|'A'..'Z'|'_'|'0'..'9')*
	;

ESCAPED_IDENT
options {
  paraphrase = &quot;an escaped identifer&quot;;
  testLiterals = false;			// redundant, but explicit is good.
}
    // NOTE: Adding a ! to the '_' doesn't seem to work,
    //       so we adjust _begin manually.

	:   '_' ('a'..'z'|'A'..'Z') ('a'..'z'|'A'..'Z'|'_'|'0'..'9')*
							{_begin++;$setType(IDENT);}
	;

</PRE>

<!--endarticle-->
    <HR>
    <P><UL>
        <!--threads-->
	<LI>Previous message: <A HREF="005983.html">New antlr-interest member
</A></li>
	<LI>Next message: <A HREF="005985.html">[antlr-interest] IDL3 Grammar
</A></li>
         <LI> <B>Messages sorted by:</B> 
              <a href="date.html#5984">[ date ]</a>
              <a href="thread.html#5984">[ thread ]</a>
              <a href="subject.html#5984">[ subject ]</a>
              <a href="author.html#5984">[ author ]</a>
         </LI>
       </UL>

<hr>
<a href="http://www.antlr.org/mailman/listinfo/antlr-interest">More information about the antlr-interest
mailing list</a><br>
</body></html>
