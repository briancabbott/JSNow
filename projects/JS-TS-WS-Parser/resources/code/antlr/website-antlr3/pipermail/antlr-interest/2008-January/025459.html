<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 3.2//EN">
<HTML>
 <HEAD>
   <TITLE> [antlr-interest] nested parsing (BSDL)
   </TITLE>
   <LINK REL="Index" HREF="index.html" >
   <LINK REL="made" HREF="mailto:antlr-interest%40antlr.org?Subject=Re:%20%5Bantlr-interest%5D%20nested%20parsing%20%28BSDL%29&In-Reply-To=%3C20080101160348.909F011EC20%40www.antlr.org%3E">
   <META NAME="robots" CONTENT="index,nofollow">
   <META http-equiv="Content-Type" content="text/html; charset=us-ascii">
   <LINK REL="Previous"  HREF="025458.html">
   <LINK REL="Next"  HREF="025460.html">
 </HEAD>
 <BODY BGCOLOR="#ffffff">
   <H1>[antlr-interest] nested parsing (BSDL)</H1>
    <B>Harald M. M&#252;ller</B> 
    <A HREF="mailto:antlr-interest%40antlr.org?Subject=Re:%20%5Bantlr-interest%5D%20nested%20parsing%20%28BSDL%29&In-Reply-To=%3C20080101160348.909F011EC20%40www.antlr.org%3E"
       TITLE="[antlr-interest] nested parsing (BSDL)">harald_m_mueller at gmx.de
       </A><BR>
    <I>Tue Jan  1 08:03:48 PST 2008</I>
    <P><UL>
        <LI>Previous message: <A HREF="025458.html">[antlr-interest] nested parsing (BSDL)
</A></li>
        <LI>Next message: <A HREF="025460.html">[antlr-interest] More, Status of C++ backend?
</A></li>
         <LI> <B>Messages sorted by:</B> 
              <a href="date.html#25459">[ date ]</a>
              <a href="thread.html#25459">[ thread ]</a>
              <a href="subject.html#25459">[ subject ]</a>
              <a href="author.html#25459">[ author ]</a>
         </LI>
       </UL>
    <HR>  
<!--beginarticle-->
<PRE>Me ... a last time ... 

Of course, you can merge my Inner and Outer grammars into one grammar; and
yet fire up a new parser for the strings. You'd end up with a single
combined grammar then!
As the example in the main program (end of email) shows, expression now
allows to write a &quot;spec&quot; inside a string as well as outside - the AST will
always be the same!
Is that acceptable to you?

... still, I more and more find this a very weird language definition; I
don't think that such an &quot;constructs inside as well as outside strings&quot; does
occur anywhere else (on the other hand, I have seen languages that were even
weirder than that - combining free-format and fixed-column format, e.g.
...).

Regards
Harald

-----------------

grammar StringNestedLanguage;

options {
	language=CSharp;
	ASTLabelType=CommonTree;
	output=AST;
}

@parser::members {
    private CommonTree CreateAST(string input) {
		// Console.Out.WriteLine(&quot;INNER: &quot; + input);
        ICharStream cs = new ANTLRStringStream(input);
        StringNestedLanguageLexer lexer = new StringNestedLanguageLexer(cs);
        CommonTokenStream ts = new CommonTokenStream(lexer);
        StringNestedLanguageParser parser = new
StringNestedLanguageParser(ts);
        return (CommonTree) parser.specInSTRING().Tree;
    }
}

// Standard entry point for single file.
top : definition*
    ;

definition
    : ATTRIBUTE^ ID OF! ID COLON! ENTITY! IS! expression SEMI!
    ;
    
expression
    : NUMBER
    | // if spec is in string.
      STRING                    -&gt; { CreateAST($STRING.text) }
    | // if spec is written directly.
      spec
    ;

// Additional entry point for in-STRING parsing.
specInSTRING
    : spec 
      EOF!
    ;

spec
    : thing 
      ( COMMA! 
        thing
      )* 
    ;
    
thing
    : ls^
      LPAR!
      param
      ( COMMA! param
      )*
      RPAR!
    ;
    
ls  : ID (LBRCKT^ NUMBER RBRCKT!)?
    | NUMBER
    ;
    
param
    : ID
    | NUMBER
    | ASTERISK
    | thing
    ;

// ------

STRING @init { string t = &quot;&quot;; }
    : s0=SIMPLE_STRING		{ t += $s0.text.Trim('\&quot;'); }
      IGNORE 
      ( '&amp;' 
        IGNORE 
        s1=SIMPLE_STRING	{ t += $s1.text.Trim('\&quot;'); }
        IGNORE
      )*
							{ $text = t; }
    ;

fragment
SIMPLE_STRING
    : '&quot;'! ~('&quot;')* '&quot;'!
    ;

fragment
IGNORE
    : ( IWS
      | INL
      | ICOMMENT
      )*
    ;

fragment
IWS  : (' '|'\t')+
    ;
    
WS   : IWS                       { $channel = HIDDEN; }
	;
	
fragment
INL  : ('\r'|'\n')+                     
    ;

NL   : INL						{ $channel = HIDDEN;
}
	;
	
fragment
ICOMMENT
    : '--' ~('\r'|'\n')* INL             
    ;
    
COMMENT   : ICOMMENT                       { $channel = HIDDEN; }
	;
	
ATTRIBUTE
    : 'attribute'
    ;

ENTITY
    : 'entity'
    ;
    
IS  : 'is'
    ;
    
OF  : 'of'
    ;

ID  : ('A'..'Z'|'a'..'z'|'_') ('A'..'Z'|'a'..'z'|'0'..'9'|'_')*
    ;
    
NUMBER
    : ('0'..'9')+
    ;

SEMI
    : ';'
    ;
    
LPAR
    : '('
    ;
    
RPAR
    : ')'
    ;
        
LBRCKT
    : '['
    ;

RBRCKT
    : ']'
    ;
    
COMMA
    : ','
    ;
    
ASTERISK
    : '*'
    ;
    
COLON
    : ':'
    ;

==============================================

using System;
using Antlr.Runtime;
using Antlr.Runtime.Tree;

namespace Interpreter {
    class Program {
        static void Main() {
            ParseAndInterpret(@&quot;
attribute BLA of ttl74bct8374 : entity is 3;

attribute BLI of ttl74bct8374 : entity is &quot;&quot;BYPASS (11111111, 10001000,
00000101, 10000100, 00000001)&quot;&quot;;

attribute BLI of ttl74bct8374 : entity is BYPASS (11111111, 10001000,
00000101, 10000100, 00000001);

attribute INSTRUCTION_OPCODE of ttl74bct8374 : entity is
        &quot;&quot;BYPASS (11111111, 10001000, 00000101, 10000100, 00000001),&quot;&quot;   &amp;
        &quot;&quot;EXTEST (00000000, 10000000),&quot;&quot;  &amp;
        &quot;&quot;TRIBYP (00000110, 10000110),&quot;&quot;  &amp;     -- Boundary Hi-Z
        &quot;&quot;SETBYP (00000111, 10000111),&quot;&quot;  &amp;     -- Boundary 1/0
        &quot;&quot;SCANCT (00001111, 10001111)&quot;&quot;;        -- BCR Scan test

attribute INSTRUCTION_OPCODE of ttl74bct8374 : entity is
        BYPASS (11111111, 10001000, 00000101, 10000100, 00000001),
        EXTEST (00000000, 10000000),
        TRIBYP (00000110, 10000110),     -- Boundary Hi-Z
        SETBYP (00000111, 10000111),     -- Boundary 1/0
        SCANCT (00001111, 10001111);        -- BCR Scan test

attribute BOUNDARY_REGISTER of ttl74bct8374 : entity is
       -- num cell  port   function safe [ccell disval rslt]
         &quot;&quot;17 (BC_1, CLK,    input,   X),&quot;&quot;  &amp;
         &quot;&quot;16 (BC_1, OC_NEG, input,   X),&quot;&quot;  &amp;  -- Merged Input/Control
         &quot;&quot;16 (BC_1, *,      control, 1),&quot;&quot;  &amp;  -- Merged Input/Control
         &quot;&quot;15 (BC_1, D(1),   input,   X),&quot;&quot;  &amp;
         &quot;&quot;8  (BC_1, D(8),   input,   X),&quot;&quot;  &amp;
         &quot;&quot;7  (BC_1, Q(1),   output3, X,  16, 1, Z),&quot;&quot; &amp;  -- cell 16 @ 1 -&gt;
Hi-Z.
         &quot;&quot;0  (BC_1, Q(8),   output3, X,  16, 1, Z)&quot;&quot;;

attribute BOUNDARY_REGISTER of ttl74bct8374 : entity is
       -- num cell  port   function safe [ccell disval rslt]
         17 (BC_1, CLK,    input,   X),
         16 (BC_1, OC_NEG, input,   X),  -- Merged Input/Control
         16 (BC_1, *,      control, 1),  -- Merged Input/Control
         15 (BC_1, D(1),   input,   X),
         8  (BC_1, D(8),   input,   X),
         7  (BC_1, Q(1),   output3, X,  16, 1, Z),  -- cell 16 @ 1 -&gt; Hi-Z.
         0  (BC_1, Q(8),   output3, X,  16, 1, Z);
&quot;);
       }

        private static void ParseAndInterpret(string input) {
            // Setup lexer and parser
            ICharStream cs = new ANTLRStringStream(input);
            StringNestedLanguageLexer lexer = new
StringNestedLanguageLexer(cs);
            CommonTokenStream ts = new CommonTokenStream(lexer);
            StringNestedLanguageParser parser = new
StringNestedLanguageParser(ts);

            // Create the AST
            CommonTree tree = (CommonTree) parser.top().Tree;
            Console.Out.WriteLine(tree.ToStringTree());
        }
    }
}

</PRE>




<!--endarticle-->
    <HR>
    <P><UL>
        <!--threads-->
	<LI>Previous message: <A HREF="025458.html">[antlr-interest] nested parsing (BSDL)
</A></li>
	<LI>Next message: <A HREF="025460.html">[antlr-interest] More, Status of C++ backend?
</A></li>
         <LI> <B>Messages sorted by:</B> 
              <a href="date.html#25459">[ date ]</a>
              <a href="thread.html#25459">[ thread ]</a>
              <a href="subject.html#25459">[ subject ]</a>
              <a href="author.html#25459">[ author ]</a>
         </LI>
       </UL>

<hr>
<a href="http://www.antlr.org/mailman/listinfo/antlr-interest">More information about the antlr-interest
mailing list</a><br>
</body></html>
