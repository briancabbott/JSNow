<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 3.2//EN">
<HTML>
 <HEAD>
   <TITLE> [antlr-interest] Antlr lexer does not try other possible matches when it fails to match a token
   </TITLE>
   <LINK REL="Index" HREF="index.html" >
   <LINK REL="made" HREF="mailto:antlr-interest%40antlr.org?Subject=Re:%20%5Bantlr-interest%5D%20Antlr%20lexer%20does%20not%20try%20other%20possible%0A%20matches%20when%20it%20fails%20to%20match%20a%20token&In-Reply-To=%3C1291225196.12003.10.camel%40gecko%3E">
   <META NAME="robots" CONTENT="index,nofollow">
   <META http-equiv="Content-Type" content="text/html; charset=us-ascii">
   <LINK REL="Previous"  HREF="040207.html">
   <LINK REL="Next"  HREF="040204.html">
 </HEAD>
 <BODY BGCOLOR="#ffffff">
   <H1>[antlr-interest] Antlr lexer does not try other possible matches when it fails to match a token</H1>
    <B>John B. Brodie</B> 
    <A HREF="mailto:antlr-interest%40antlr.org?Subject=Re:%20%5Bantlr-interest%5D%20Antlr%20lexer%20does%20not%20try%20other%20possible%0A%20matches%20when%20it%20fails%20to%20match%20a%20token&In-Reply-To=%3C1291225196.12003.10.camel%40gecko%3E"
       TITLE="[antlr-interest] Antlr lexer does not try other possible matches when it fails to match a token">jbb at acm.org
       </A><BR>
    <I>Wed Dec  1 09:39:56 PST 2010</I>
    <P><UL>
        <LI>Previous message: <A HREF="040207.html">[antlr-interest] Antlr lexer does not try other possible matches when it fails to match a token
</A></li>
        <LI>Next message: <A HREF="040204.html">[antlr-interest] Island grammar for reading shell commands
</A></li>
         <LI> <B>Messages sorted by:</B> 
              <a href="date.html#40212">[ date ]</a>
              <a href="thread.html#40212">[ thread ]</a>
              <a href="subject.html#40212">[ subject ]</a>
              <a href="author.html#40212">[ author ]</a>
         </LI>
       </UL>
    <HR>  
<!--beginarticle-->
<PRE>Greetings!

On Wed, 2010-12-01 at 16:09 +0100, COUJOULOU, Philippe wrote:
&gt;<i> Sorry, the code I posted was not correct. Here is the correct code I wanted to copy in my previous message (with xxxParameter parser rules referring to correct parameter value rule):
</I>&gt;<i> 
</I>&gt;<i> grammar test;
</I>&gt;<i> 
</I>&gt;<i> listOfParameters           :           parameterDef (CRLF parameterDef)* EOF;
</I>&gt;<i> 
</I>&gt;<i> parameterDef    : stationParameter|addressParameter|locationParameter|sessionParameter|providerParameter|codeParameter|dateParameter|timeParameter;
</I>&gt;<i> 
</I>&gt;<i> stationParameter           :           STATION SPACE stationName;
</I>&gt;<i> stationName                  :           LETTER LETTER LETTER DIGIT;
</I>&gt;<i> 
</I>&gt;<i> addressParameter         :           ADDRESS SPACE address;
</I>&gt;<i> address                                    :           (LETTER|DIGIT) (LETTER|DIGIT) (LETTER|DIGIT) (LETTER|DIGIT) (LETTER|DIGIT) (LETTER|DIGIT) (LETTER|DIGIT);
</I>&gt;<i> 
</I>&gt;<i> locationParameter         :           LOCATION SPACE location;
</I>&gt;<i> location                        :           LETTER LETTER LETTER LETTER;
</I>&gt;<i> 
</I>&gt;<i> sessionParameter          :           SESSION SPACE session;
</I>&gt;<i> session                         :           LETTER LETTER DIGIT DIGIT DIGIT DIGIT;
</I>&gt;<i> 
</I>&gt;<i> providerParameter         :           PROVIDER SPACE provider;
</I>&gt;<i> provider                        :           LETTER LETTER LETTER;
</I>&gt;<i> 
</I>&gt;<i> codeParameter              :           CODE SPACE code;
</I>&gt;<i> code                             :           LETTER (LETTER|DIGIT) (LETTER|DIGIT);
</I>&gt;<i> 
</I>&gt;<i> dateParameter               :           DATE SPACE stationName;
</I>
i assume that stationName here should be date

&gt;<i> date                              :           DIGIT DIGIT DIGIT DIGIT DIGIT DIGIT;
</I>&gt;<i> 
</I>&gt;<i> timeParameter               :           TIME SPACE time;
</I>&gt;<i> time                              :           (DIGIT DIGIT DIGIT DIGIT) | (DASH DASH DASH DASH) | (SPACE SPACE SPACE SPACE);
</I>&gt;<i> 
</I>&gt;<i> 
</I>&gt;<i> STATION           :           'STATION';
</I>&gt;<i> ADDRESS        :           'ADDRESS';
</I>&gt;<i> LOCATION:       'LOCATION';
</I>&gt;<i> SESSION          :           'SESSION';
</I>&gt;<i> PROVIDER:      'PROVIDER';
</I>&gt;<i> CODE   :           'CODE';
</I>&gt;<i> DATE   :           'DATE';
</I>&gt;<i> TIME     :           'TIME';
</I>&gt;<i> LETTER            :           'a'..'z' | 'A'..'Z';
</I>&gt;<i> DIGIT    :           '0'..'9';
</I>&gt;<i> DASH   :           '-';
</I>&gt;<i> SPACE :           ' ';
</I>&gt;<i> CRLF : '\r'? '\n';
</I>&gt;<i> 
</I>&gt;<i> 
</I>
I do not think that ANTLR v3 Lexers are able to backtrack in the fashion
you want. e.g. when seeing the ST in EST1 be able to issue two LETTER
tokens rather that insisting on finding a STATION token.

3 solutions come to mind --- but perhaps none of them will be
appropriate for you since my solutions are based soley upon the fragment
of your entire grammar that you have posted and therefore might not work
out for the whole.

1) do not use ANTLR. just divide the line at the first SPACE. lookup the
first portion in a keyword hashmap and dispatch to proper code to handle
the second portion.

2) move each parameter to the Lexer and let the Lexer work out the
STATION vs E S T 1 issues. see the attached AllLexer.g for a tested
example of what I am suggesting here.

3) move everything into the Parser, e.g. the Lexer just issues single
character tokens, and the Parser works out how to glue things together.
see the attached NoLexer.g for a tested example of what I am suggesting
here.

Hope this helps
   -jbb


&gt;<i> 
</I>&gt;<i> From: COUJOULOU, Philippe
</I>&gt;<i> Sent: 01 December 2010 16:04
</I>&gt;<i> To: 'Nick Vlassopoulos'
</I>&gt;<i> Cc: <A HREF="http://www.antlr.org/mailman/listinfo/antlr-interest">antlr-interest at antlr.org</A>
</I>&gt;<i> Subject: RE: [antlr-interest] Antlr lexer does not try other possible matches when it fails to match a token
</I>&gt;<i> 
</I>&gt;<i> 
</I>&gt;<i> Thanks for your quick response Nick.
</I>&gt;<i> 
</I>&gt;<i> You are right, it works with the example but I am afraid this is not feasible with my complete grammar.
</I>&gt;<i> 
</I>&gt;<i>  If I do that for all my possible parameter values (the format depends on the preceding parameter name), I would have a lot of lexer rules to sort out and that would for sure be conflicting:
</I>&gt;<i> 
</I>&gt;<i> STATION_NAME           :           LETTER LETTER LETTER DIGIT;
</I>&gt;<i> ADDRESS                    :           (LETTER|DIGIT) (LETTER|DIGIT) (LETTER|DIGIT) (LETTER|DIGIT) (LETTER|DIGIT) (LETTER|DIGIT) (LETTER|DIGIT);
</I>&gt;<i> LOCATION                    :           LETTER LETTER LETTER LETTER;
</I>&gt;<i> SESSION                      :           LETTER LETTER DIGIT DIGIT DIGIT DIGIT;
</I>&gt;<i> PROVIDER                   :           LETTER LETTER LETTER;
</I>&gt;<i> CODE                           :           LETTER (LETTER|DIGIT) (LETTER|DIGIT);
</I>&gt;<i> DATE                           :           DIGIT DIGIT DIGIT DIGIT DIGIT DIGIT;
</I>&gt;<i> TIME                             :           (DIGIT DIGIT DIGIT DIGIT) | (DASH DASH DASH DASH) | (SPACE SPACE SPACE SPACE);
</I>&gt;<i> ...
</I>&gt;<i> 
</I>&gt;<i> 
</I>&gt;<i> I think it is much preferable to have the lexer returning a sequence of DIGIT and LETTERS (except for param names), and to specify what is the expected sequence for a given parameter at parsing level. Something like that (but again this is an extract):
</I>&gt;<i> 
</I>&gt;<i> grammar test;
</I>&gt;<i> 
</I>&gt;<i> listOfParameters           :           parameterDef (CRLF parameterDef)* EOF;
</I>&gt;<i> 
</I>&gt;<i> parameterDef                : stationParameter|addressParameter|locationParameter|sessionParameter|providerParameter|codeParameter|dateParameter|timeParameter;
</I>&gt;<i> 
</I>&gt;<i> stationParameter           :           STATION SPACE stationName;
</I>&gt;<i> stationName                  :           LETTER LETTER LETTER DIGIT;
</I>&gt;<i> 
</I>&gt;<i> addressParameter         :           ADDRESS SPACE stationName;
</I>&gt;<i> adress                          :           (LETTER|DIGIT) (LETTER|DIGIT) (LETTER|DIGIT) (LETTER|DIGIT) (LETTER|DIGIT) (LETTER|DIGIT) (LETTER|DIGIT);
</I>&gt;<i> 
</I>&gt;<i> locationParameter         :           LOCATION SPACE stationName;
</I>&gt;<i> location                        :           LETTER LETTER LETTER LETTER;
</I>&gt;<i> 
</I>&gt;<i> sessionParameter          :           SESSION SPACE stationName;
</I>&gt;<i> session                         :           LETTER LETTER DIGIT DIGIT DIGIT DIGIT;
</I>&gt;<i> 
</I>&gt;<i> providerParameter         :           PROVIDER SPACE stationName;
</I>&gt;<i> provider                        :           LETTER LETTER LETTER;
</I>&gt;<i> 
</I>&gt;<i> codeParameter              :           CODE SPACE stationName;
</I>&gt;<i> code                             :           LETTER (LETTER|DIGIT) (LETTER|DIGIT);
</I>&gt;<i> 
</I>&gt;<i> dateParameter               :           DATE SPACE stationName;
</I>&gt;<i> date                              :           DIGIT DIGIT DIGIT DIGIT DIGIT DIGIT;
</I>&gt;<i> 
</I>&gt;<i> timeParameter               :           TIME SPACE stationName;
</I>&gt;<i> time                              :           (DIGIT DIGIT DIGIT DIGIT) | (DASH DASH DASH DASH) | (SPACE SPACE SPACE SPACE);
</I>&gt;<i> 
</I>&gt;<i> 
</I>&gt;<i> STATION           :           'STATION';
</I>&gt;<i> ADDRESS        :           'ADDRESS';
</I>&gt;<i> LOCATION:       'LOCATION';
</I>&gt;<i> SESSION          :           'SESSION';
</I>&gt;<i> PROVIDER:      'PROVIDER';
</I>&gt;<i> CODE   :           'CODE';
</I>&gt;<i> DATE   :           'DATE';
</I>&gt;<i> TIME     :           'TIME';
</I>&gt;<i> LETTER            :           'a'..'z' | 'A'..'Z';
</I>&gt;<i> DIGIT    :           '0'..'9';
</I>&gt;<i> DASH   :           '-';
</I>&gt;<i> SPACE :           ' ';
</I>&gt;<i> CRLF : '\r'? '\n';
</I>&gt;<i> 
</I>&gt;<i> 
</I>&gt;<i> 
</I>&gt;<i> 
</I>&gt;<i> 
</I>&gt;<i> 
</I>&gt;<i> From: Nick Vlassopoulos [mailto:<A HREF="http://www.antlr.org/mailman/listinfo/antlr-interest">nvlassopoulos at gmail.com</A>]
</I>&gt;<i> Sent: 01 December 2010 15:10
</I>&gt;<i> To: COUJOULOU, Philippe
</I>&gt;<i> Cc: <A HREF="http://www.antlr.org/mailman/listinfo/antlr-interest">antlr-interest at antlr.org</A>
</I>&gt;<i> Subject: Re: [antlr-interest] Antlr lexer does not try other possible matches when it fails to match a token
</I>&gt;<i> 
</I>&gt;<i> Hello Philippe,
</I>&gt;<i> 
</I>&gt;<i> Although I am not an expert, I thing you should let the lexer sort out
</I>&gt;<i> the &quot;3 letters 1 digit&quot; in the station name. Alternatively, you could probably
</I>&gt;<i> add the station name as an identifier and check if it is in the correct format
</I>&gt;<i> after parsing it.
</I>&gt;<i> 
</I>&gt;<i> Without being sure if it is a good solution, the following seems to work:
</I>&gt;<i> 
</I>&gt;<i> Best regards,
</I>&gt;<i> 
</I>&gt;<i> Nikos
</I>&gt;<i> 
</I>&gt;<i> -------------------------
</I>&gt;<i> grammar Stations;
</I>&gt;<i> 
</I>&gt;<i> stationParameter         :
</I>&gt;<i>             KEYWORD_STATION SPACE stationName;
</I>&gt;<i> 
</I>&gt;<i> stationName
</I>&gt;<i>             :           STATION_NAME;
</I>&gt;<i> 
</I>&gt;<i> STATION_NAME
</I>&gt;<i>             :           LETTER LETTER LETTER DIGIT;
</I>&gt;<i> 
</I>&gt;<i> KEYWORD_STATION        :           'STATION';
</I>&gt;<i> LETTER                     :           'a'..'z' | 'A'..'Z';
</I>&gt;<i> DIGIT             :           '0'..'9';
</I>&gt;<i> SPACE                       :           ' ';
</I>&gt;<i> -------------------------
</I>&gt;<i> 
</I>&gt;<i> 
</I>&gt;<i> On Wed, Dec 1, 2010 at 2:18 PM, COUJOULOU, Philippe &lt;<A HREF="http://www.antlr.org/mailman/listinfo/antlr-interest">philippe.coujoulou at airbus.com</A>&lt;mailto:<A HREF="http://www.antlr.org/mailman/listinfo/antlr-interest">philippe.coujoulou at airbus.com</A>&gt;&gt; wrote:
</I>&gt;<i> Dear all,
</I>&gt;<i> 
</I>&gt;<i> I am trying to parse a message that contains parameters values like &lt;PARAM_NAME&gt; &lt;VALUE&gt;, for instance &quot;STATION EST1&quot;.
</I>&gt;<i> Here is a very simple extract of my grammar for one of these parameters (the one given in the above example):
</I>&gt;<i> 
</I>&gt;<i> grammar test;
</I>&gt;<i> 
</I>&gt;<i> KEYWORD_STATION :       'STATION';
</I>&gt;<i> DIGIT    :        '0'..'9';
</I>&gt;<i> LETTER  :        'a'..'z' | 'A'..'Z';
</I>&gt;<i> SPACE   :       ' ';
</I>&gt;<i> 
</I>&gt;<i> stationParameter        :       KEYWORD_STATION SPACE stationName;
</I>&gt;<i> stationName     :       LETTER LETTER LETTER DIGIT;
</I>&gt;<i> 
</I>&gt;<i> 
</I>&gt;<i> The point is that when I try to parse my example message (STATION EST1), I get a MismatchTokenException at the point where the parser attempts to read the last &quot;ST1&quot;. After some analysis, I understood that the lexer generated the following tokens: KEYWORD_STATION SPACE LETTER for the string &quot;STATION E&quot;  and then attempted to match the remaining &quot;ST1&quot; with KEYWORD_STATION but failed to complete it.
</I>&gt;<i> 
</I>&gt;<i> At this point, I would expect the lexer to backtrack to the beginning of 'ST1' and then match it with LETTER LETTER DIGIT, but it doesn't.
</I>&gt;<i> 
</I>&gt;<i> I have tried various combinations of &quot;backtrack&quot;, &quot;memorize&quot; and &quot;k&quot; options without any success. I must have missed something. (Should it help, I use ANTLRWorks 1.4).
</I>&gt;<i> 
</I>&gt;<i> Please could you tell me how to proceed in order to make the lexer backtrack and try other alternatives when a keyword of my language is not exactly matched ?
</I>&gt;<i> 
</I>&gt;<i> Thanks in advance for your help.
</I>&gt;<i> 
</I>&gt;<i> Best Regards,
</I>&gt;<i> 
</I>&gt;<i> Philippe Coujoulou.
</I>
-------------- next part --------------
grammar AllLexer;

options {
   output = AST;
   ASTLabelType = CommonTree;
}

@members {
   private static final String [] x = new String[] {
      &quot;STATION ABC2\nADDRESS 1234567&quot;,&quot;STATION STA1&quot;
   };

   public static void main(String [] args) {
      for( int i = 0; i &lt; x.length; ++i ) {
         try {
            System.out.println(&quot;about to parse:`&quot;+x[i]+&quot;`&quot;);
            AllLexerLexer lexer =
               new AllLexerLexer(new ANTLRStringStream(x[i]));
            CommonTokenStream tokens = new CommonTokenStream(lexer);
            System.out.println(&quot;tokens:&quot;+tokens.toString());

            AllLexerParser parser = new AllLexerParser(tokens);
            AllLexerParser.listOfParameters_return p_result =
               parser.listOfParameters();

            CommonTree ast = p_result.tree;
            if( ast == null ) {
               System.out.println(&quot;resultant tree: is NULL&quot;);
            } else {
               System.out.println(&quot;resultant tree: &quot; + ast.toStringTree());
            }
            System.out.println();
         } catch(Exception e) {
            e.printStackTrace();
         }
      }
   }
}

listOfParameters : parameterDef (CRLF parameterDef)* EOF;

parameterDef
   : StationParameter
   | AddressParameter
   | LocationParameter
   | SessionParameter
   | ProviderParameter
   | CodeParameter
   | DateParameter
   | TimeParameter;

StationParameter : STATION SPACE StationName;
fragment StationName : LETTER LETTER LETTER DIGIT;

AddressParameter : ADDRESS SPACE Address;
fragment Address : L_or_D L_or_D L_or_D L_or_D L_or_D L_or_D L_or_D;

LocationParameter : LOCATION SPACE Location;
fragment Location : LETTER LETTER LETTER LETTER;

SessionParameter : SESSION SPACE Session;
fragment Session : LETTER LETTER DIGIT DIGIT DIGIT DIGIT;

ProviderParameter : PROVIDER SPACE Provider;
fragment Provider : LETTER LETTER LETTER;

CodeParameter : CODE SPACE Code;
fragment Code : LETTER L_or_D L_or_D;

DateParameter : DATE SPACE Date;
fragment Date : DIGIT DIGIT DIGIT DIGIT DIGIT DIGIT;

TimeParameter : TIME SPACE Time;
fragment Time : (DIGIT DIGIT DIGIT DIGIT) | (DASH DASH DASH DASH) | (SPACE SPACE SPACE SPACE);

fragment L_or_D  : LETTER | DIGIT ;

fragment STATION : 'STATION';
fragment ADDRESS : 'ADDRESS';
fragment LOCATION: 'LOCATION';
fragment SESSION : 'SESSION';
fragment PROVIDER: 'PROVIDER';
fragment CODE    : 'CODE';
fragment DATE    : 'DATE';
fragment TIME    : 'TIME';
fragment LETTER  : 'a'..'z' | 'A'..'Z';
fragment DIGIT   : '0'..'9';
fragment DASH    : '-';
fragment SPACE   : ' ';

CRLF : '\r'? '\n';


-------------- next part --------------
grammar NoLexer;

options {
   output = AST;
   ASTLabelType = CommonTree;
}

@members {
   private static final String [] x = new String[] {
      &quot;STATION ABC2\nADDRESS 1234567&quot;,&quot;STATION STA1&quot;
   };

   public static void main(String [] args) {
      for( int i = 0; i &lt; x.length; ++i ) {
         try {
            System.out.println(&quot;about to parse:`&quot;+x[i]+&quot;`&quot;);
            NoLexerLexer lexer =
               new NoLexerLexer(new ANTLRStringStream(x[i]));
            CommonTokenStream tokens = new CommonTokenStream(lexer);
            System.out.println(&quot;tokens:&quot;+tokens.toString());

            NoLexerParser parser = new NoLexerParser(tokens);
            NoLexerParser.listOfParameters_return p_result =
               parser.listOfParameters();

            CommonTree ast = p_result.tree;
            if( ast == null ) {
               System.out.println(&quot;resultant tree: is NULL&quot;);
            } else {
               System.out.println(&quot;resultant tree: &quot; + ast.toStringTree());
            }
            System.out.println();
         } catch(Exception e) {
            e.printStackTrace();
         }
      }
   }
}

listOfParameters : parameterDef (CRLF parameterDef)* EOF;

parameterDef
   : stationParameter
   | addressParameter
   | locationParameter
   | sessionParameter
   | providerParameter
   | codeParameter
   | dateParameter
   | timeParameter;

stationParameter : stationKeyword SPACE stationName;
stationName : letter letter letter digit;

addressParameter : addressKeyword SPACE addressValue;
addressValue : l_or_d l_or_d l_or_d l_or_d l_or_d l_or_d l_or_d;

locationParameter : locationKeyword SPACE locationValue;
locationValue : letter letter letter letter;

sessionParameter : sessionKeyword SPACE sessionValue;
sessionValue : letter letter digit digit digit digit;

providerParameter : providerKeyword SPACE providerValue;
providerValue : letter letter letter;

codeParameter : codeKeyword SPACE codeValue;
codeValue : letter l_or_d l_or_d;

dateParameter : dateKeyword SPACE dateValue;
dateValue : digit digit digit digit digit digit;

timeParameter : timeKeyword SPACE timeValue;
timeValue : (digit digit digit digit) | (DASH DASH DASH DASH) | (SPACE SPACE SPACE SPACE);

l_or_d  : letter | digit ;

digit   : ZERO | ONE | TWO | THREE | FOUR | FIVE | SIX | SEVEN | EIGHT | NINE ;

letter  : A|B|C|D|E|F|G|H|I|J|K|L|M|N|O|P|Q|R|S|T|U|V|W|X|Y|Z ;

stationKeyword : S T A T I O N; //NB: accepts &quot;StaTiON&quot; as a keyword
addressKeyword : A D D R E S S;
locationKeyword: L O C A T I O N;
sessionKeyword : S E S S I O N;
providerKeyword: P R O V I D E R;
codeKeyword    : C O D E;
dateKeyword    : D A T E;
timeKeyword    : T I M E;

// the following makes the lexer essentially case insensitive.

// if case is important, replace the A rule with 2 rules A_Upper:'A';
// and A_Lower:'a'; and appropriately update the parser. repeat for
// all the alphabet.

A : 'a' | 'A' ;
B : 'b' | 'B' ;
C : 'c' | 'C' ;
D : 'd' | 'D' ;
E : 'e' | 'E' ;
F : 'f' | 'F' ;
G : 'g' | 'G' ;
H : 'h' | 'H' ;
I : 'i' | 'I' ;
J : 'j' | 'J' ;
K : 'k' | 'K' ;
L : 'l' | 'L' ;
M : 'm' | 'M' ;
N : 'n' | 'N' ;
O : 'o' | 'O' ;
P : 'p' | 'P' ;
Q : 'q' | 'Q' ;
R : 'r' | 'R' ;
S : 's' | 'S' ;
T : 't' | 'T' ;
U : 'u' | 'U' ;
V : 'v' | 'V' ;
W : 'w' | 'W' ;
X : 'x' | 'X' ;
Y : 'y' | 'Y' ;
Z : 'z' | 'Z' ;

ZERO  : '0' ;
ONE   : '1' ;
TWO   : '2' ;
THREE : '3' ;
FOUR  : '4' ;
FIVE  : '5' ; 
SIX   : '6' ;
SEVEN : '7' ;
EIGHT : '8' ;
NINE  : '9' ;

DASH    : '-';
SPACE   : ' ';

CRLF : '\r'? '\n';


</PRE>








<!--endarticle-->
    <HR>
    <P><UL>
        <!--threads-->
	<LI>Previous message: <A HREF="040207.html">[antlr-interest] Antlr lexer does not try other possible matches when it fails to match a token
</A></li>
	<LI>Next message: <A HREF="040204.html">[antlr-interest] Island grammar for reading shell commands
</A></li>
         <LI> <B>Messages sorted by:</B> 
              <a href="date.html#40212">[ date ]</a>
              <a href="thread.html#40212">[ thread ]</a>
              <a href="subject.html#40212">[ subject ]</a>
              <a href="author.html#40212">[ author ]</a>
         </LI>
       </UL>

<hr>
<a href="http://www.antlr.org/mailman/listinfo/antlr-interest">More information about the antlr-interest
mailing list</a><br>
</body></html>
