<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 3.2//EN">
<HTML>
 <HEAD>
   <TITLE> [antlr-interest] Antlrworks 1.4.1,	OS X 10.6.5 and debugging issues
   </TITLE>
   <LINK REL="Index" HREF="index.html" >
   <LINK REL="made" HREF="mailto:antlr-interest%40antlr.org?Subject=Re:%20%5Bantlr-interest%5D%20Antlrworks%201.4.1%2C%0A%09OS%20X%2010.6.5%20and%20debugging%20issues&In-Reply-To=%3C424B0EE9-B13B-49E2-B297-F9C60D425F58%40internode.on.net%3E">
   <META NAME="robots" CONTENT="index,nofollow">
   <META http-equiv="Content-Type" content="text/html; charset=us-ascii">
   <LINK REL="Previous"  HREF="040321.html">
   <LINK REL="Next"  HREF="040327.html">
 </HEAD>
 <BODY BGCOLOR="#ffffff">
   <H1>[antlr-interest] Antlrworks 1.4.1,	OS X 10.6.5 and debugging issues</H1>
    <B>Christopher Hunt</B> 
    <A HREF="mailto:antlr-interest%40antlr.org?Subject=Re:%20%5Bantlr-interest%5D%20Antlrworks%201.4.1%2C%0A%09OS%20X%2010.6.5%20and%20debugging%20issues&In-Reply-To=%3C424B0EE9-B13B-49E2-B297-F9C60D425F58%40internode.on.net%3E"
       TITLE="[antlr-interest] Antlrworks 1.4.1,	OS X 10.6.5 and debugging issues">huntc at internode.on.net
       </A><BR>
    <I>Sun Dec 12 14:55:41 PST 2010</I>
    <P><UL>
        <LI>Previous message: <A HREF="040321.html">[antlr-interest] Antlrworks 1.4.1,	OS X 10.6.5 and debugging issues
</A></li>
        <LI>Next message: <A HREF="040327.html">[antlr-interest] Antlrworks 1.4.1,	OS X 10.6.5 and debugging issues
</A></li>
         <LI> <B>Messages sorted by:</B> 
              <a href="date.html#40326">[ date ]</a>
              <a href="thread.html#40326">[ thread ]</a>
              <a href="subject.html#40326">[ subject ]</a>
              <a href="author.html#40326">[ author ]</a>
         </LI>
       </UL>
    <HR>  
<!--beginarticle-->
<PRE>Just out of interest, I reverted to 1.4 and had the same problem. So, while there may be some other problem with 1.4.1, there is probably and issue with my grammar. Here's the grammar so far in case anyone has thoughts (it parses the zoneinfo database (1) - I'll release this to the ANTLR site when finished):

/*
 * Copyright 2010 Class Action P/L
 *
 * Licensed under the Apache License, Version 2.0 (the &quot;License&quot;);
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      <A HREF="http://www.apache.org/licenses/LICENSE-2.0">http://www.apache.org/licenses/LICENSE-2.0</A>
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an &quot;AS IS&quot; BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
 
grammar Zoneinfo;

options {
	output=AST;
	ASTLabelType=CommonTree;
	language=Java;
}
	
@header {
	package com.classactionpl.tz;
}

@lexer::header {
	package com.classactionpl.tz;
}

line
	: (ruleLine | zoneLine | linkLine | NEWLINE)*
	;

ruleLine
	: 'Rule' ruleName ruleFrom ruleTo ruleType ruleIn ruleOn ruleAt ruleSave ruleLetters NEWLINE
	;

zoneLine
	: 'Zone' zoneName zoneGmtOffset zoneRules zoneFormat 
	(zoneUntil NEWLINE+ zoneGmtOffset zoneRules zoneFormat)* 
	NEWLINE
	;

linkLine
	: 'Link' linkFrom linkTo NEWLINE
	;

ruleName
	: ID
	;
	
ruleFrom
	: INT | YEAR_MINMAX
	;
	
ruleTo
	: INT | YEAR_MINMAX | ONLY_YEAR
	;
	
ruleType
	: '-' | YEAR_TYPE
	;
	
ruleIn
	: MONTH
	;
	
ruleOn
	: INT | LAST_DAY | (DAY_OF_WEEK DAY_COMPARATOR INT)
	;
	
ruleAt
	: '-' | (positiveTime TIME_OF_DAY_SUFFIX?)
	;
	
ruleSave
	: '-' | positiveTime
	;
	
ruleLetters
	: '-' | ID
	;

zoneName
	: ID
	;

zoneGmtOffset
	: offset
	;
	
zoneRules
	: '-' | positiveTime | ID
	;
	
zoneFormat
	: ID ('/' ID)?
	;

zoneUntil
	: INT (ruleIn (ruleOn ruleAt?)?)?
	;

linkFrom
	: ID
	;
	
linkTo
	: ID
	;
	
offset
	: '-'? positiveTime
	;

positiveTime
	: INT (':' INT (':' INT)?)?
	;

YEAR_MINMAX
	: 'min' | 'max'
	;

ONLY_YEAR
	: 'only'
	;
	
YEAR_TYPE
	: 'even' | 'odd' | 'uspres' | 'nonpres' | 'nonuspres'
	;

MONTH
	: 'Jan' 'uary'? 
	| 'Feb' 'ruary'? 
	| 'Mar' 'ch'? 
	| 'Apr' 'il'? 
	| 'May' 
	| 'Jun' 'e'? 
	| 'Jul' 'y'? 
	| 'Aug' 'ust'? 
	| 'Sep' 'tember'? 
	| 'Oct' 'ober'? 
	| 'Nov' 'ember'? 
	| 'Dec' 'ember'?
	;

LAST_DAY
	: 'last' '-'? DAY_OF_WEEK
	;

DAY_OF_WEEK 
	: 'Sun' 'day'? 
	| 'Mon' 'day'? 
	| 'Tue' 'sday'? 
	| 'Wed' 'nesday'? 
	| 'Thu' 'rsday'? 
	| 'Fri' 'day'? 
	| 'Sat' 'urday'?
	;
	
DAY_COMPARATOR
	: '&lt;=' | '&gt;='
	;

TIME_OF_DAY_SUFFIX
	: 'w' | 's' | 'u' | 'g' | 'z'
	;

INT
	: ('0'..'9')+
	;

ID
	: ('a'..'z'|'A'..'Z'|'%')('a'..'z'|'A'..'Z'|'0'..'9'|'%'|'/'|'_'|'-')*
	;

NEWLINE
	: ('#' ~('\n'|'\r')*)? '\r'? '\n' | EOF
    	;

WS
    	: (' ' | '\t') {skip();}
    	;


Kind regards,
Christopher

(1) <A HREF="http://www.twinsun.com/tz/tz-link.htm">http://www.twinsun.com/tz/tz-link.htm</A>

On 10/12/2010, at 10:08 PM, Christopher Hunt wrote:

&gt;<i> Hi there,
</I>&gt;<i> 
</I>&gt;<i> I'm using Antlrworks v.1.4.1 on Mac OS X 10.6.5 and find that I cannot debug my grammar, despite all being well via the interpreter.
</I>&gt;<i> 
</I>&gt;<i> Here are my steps:
</I>&gt;<i> 1. Open a grammar
</I>&gt;<i> 2. Run/Debug...
</I>&gt;<i> 3. Select a file as input (makes no difference if I use the text option) and confirm
</I>&gt;<i> 
</I>&gt;<i> I then receive the generating/connecting messages and note that a socket connection is established i.e.:
</I>&gt;<i> 
</I>&gt;<i> netstat -an|grep 49100
</I>&gt;<i> tcp4       0      0  127.0.0.1.49100        127.0.0.1.60879        ESTABLISHED
</I>&gt;<i> tcp4      27      0  127.0.0.1.60879        127.0.0.1.49100        ESTABLISHED
</I>&gt;<i> tcp46      0      0  *.49100                *.*                    LISTEN
</I>&gt;<i> 
</I>&gt;<i> Clicking on any of the play buttons (step, step over etc.), appears to do nothing. The only event shown is &quot;commence&quot;. If I stop the debugger I have to click the stop button twice whereupon it force quits (a single stop doesn't appear to do anything).
</I>&gt;<i> 
</I>&gt;<i> Are there known issues with my configuration?
</I>&gt;<i> 
</I>&gt;<i> Thanks for your help.
</I>&gt;<i> 
</I>&gt;<i> Kind regards,
</I>&gt;<i> Christopher
</I>
</PRE>


<!--endarticle-->
    <HR>
    <P><UL>
        <!--threads-->
	<LI>Previous message: <A HREF="040321.html">[antlr-interest] Antlrworks 1.4.1,	OS X 10.6.5 and debugging issues
</A></li>
	<LI>Next message: <A HREF="040327.html">[antlr-interest] Antlrworks 1.4.1,	OS X 10.6.5 and debugging issues
</A></li>
         <LI> <B>Messages sorted by:</B> 
              <a href="date.html#40326">[ date ]</a>
              <a href="thread.html#40326">[ thread ]</a>
              <a href="subject.html#40326">[ subject ]</a>
              <a href="author.html#40326">[ author ]</a>
         </LI>
       </UL>

<hr>
<a href="http://www.antlr.org/mailman/listinfo/antlr-interest">More information about the antlr-interest
mailing list</a><br>
</body></html>
