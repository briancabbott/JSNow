<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 3.2//EN">
<HTML>
 <HEAD>
   <TITLE> [antlr-interest] how to print tokens?
   </TITLE>
   <LINK REL="Index" HREF="index.html" >
   <LINK REL="made" HREF="mailto:antlr-interest%40antlr.org?Subject=%5Bantlr-interest%5D%20how%20to%20print%20tokens%3F&In-Reply-To=000001c55767%24755a5730%240500a8c0%40nigel">
   <META NAME="robots" CONTENT="index,nofollow">
   <META http-equiv="Content-Type" content="text/html; charset=us-ascii">
   <LINK REL="Previous"  HREF="012147.html">
   <LINK REL="Next"  HREF="012154.html">
 </HEAD>
 <BODY BGCOLOR="#ffffff">
   <H1>[antlr-interest] how to print tokens?</H1>
    <B>Michael West</B> 
    <A HREF="mailto:antlr-interest%40antlr.org?Subject=%5Bantlr-interest%5D%20how%20to%20print%20tokens%3F&In-Reply-To=000001c55767%24755a5730%240500a8c0%40nigel"
       TITLE="[antlr-interest] how to print tokens?">quagly at mitzit.net
       </A><BR>
    <I>Sat May 14 06:41:33 PDT 2005</I>
    <P><UL>
        <LI>Previous message: <A HREF="012147.html">[antlr-interest] how to print tokens?
</A></li>
        <LI>Next message: <A HREF="012154.html">[antlr-interest] how to print tokens?
</A></li>
         <LI> <B>Messages sorted by:</B> 
              <a href="date.html#12153">[ date ]</a>
              <a href="thread.html#12153">[ thread ]</a>
              <a href="subject.html#12153">[ subject ]</a>
              <a href="author.html#12153">[ author ]</a>
         </LI>
       </UL>
    <HR>  
<!--beginarticle-->
<PRE>&gt;<i> Whoops... it's for C#, not Java... see the attachments. That should get you
</I>&gt;<i> started...
</I>&gt;<i> 
</I>&gt;<i> Nigel
</I>
I am using Java with this grammer, I just removed the C# option.  I
didn't see any code in the grammer so I figured this was fine. 

I see what you are doing by passing the values up the rule chain, that
makes sense.  Haven't got it to complile cleanly yet though.  Attached it my
modified grammer.

The output of the antlr.Tool looks fine to me:
------------
java antlr.Tool sql.g
ANTLR Parser Generator   Version 2.7.5 (20050509)   1989-2005 jGuru.com
sql.g:105:5: warning:Rule 'identifier' returns a value
sql.g:106:12: warning:Rule 'identifier' returns a value
sql.g:140:26: warning:Rule 'dbObject' returns a value
sql.g:141:26: warning:Rule 'dbObject' returns a value
sql.g:339:16: warning:Rule 'identifier' returns a value
sql.g:340:33: warning:Rule 'identifier' returns a value
sql.g:263:7: warning:Rule 'identifier' returns a value
sql.g:279:7: warning:Rule 'dbObject' returns a value
sql.g:272:7: warning:Rule 'dbObject' returns a value
sql.g:254:11: warning:Rule 'identifier' returns a value
sql.g:199:9: warning:Rule 'dbObject' returns a value
sql.g:199:25: warning:Rule 'dbObject' returns a value
sql.g:313:6: warning:Rule 'dbObject' returns a value
sql.g:228:19: warning:Rule 'identifier' returns a value
sql.g:228:50: warning:Rule 'identifier' returns a value
sql.g:229:23: warning:Rule 'identifier' returns a value
sql.g:248:15: warning:Rule 'identifier' returns a value
sql.g:302:7: warning:Rule 'dbObject' returns a value
sql.g:331:33: warning:Rule 'identifier' returns a value
------------

But when I compile I get:
-----------
javac -classpath '.:/usr/local/lib/antlr.jar' *.java

SqlParser.java:4504: cannot resolve symbol
symbol  : variable s
location: class SqlParser
     s=dbObject();

SqlParser.java:4506:
cannot resolve symbol 
symbol : variable s 
location: class SqlParser
     System.out.println(s);
---------

Line 4504 is just the  s=dbObject(); part of the subTableSource method.  I don't see why this error. declaring s is the first thing the method does:

public final void subTableSource() throws RecognitionException,
TokenStreamException {
try {      // for error handling
  if ( inputState.guessing==0 ) {
     String s=null;
  }
  ...


-------------- next part --------------
// Version: 0.8
// ANTLR Version: 2.7.2
// Date: 2003.08.25
//
// Description: This is a MS SQL Server 2000 SELECT statement grammar.
//
// =======================================================================================
// Author: Tomasz Jastrzebski
// Contact: <A HREF="http://www.antlr.org/mailman/listinfo/antlr-interest">tdjastrzebski at yahoo.com</A>
// Working parser/lexer generated based on this grammar will available for some time at:
// <A HREF="http://jastrzebski.europe.webmatrixhosting.net/mssqlparser.aspx">http://jastrzebski.europe.webmatrixhosting.net/mssqlparser.aspx</A> 

//options {
    //language = &quot;CSharp&quot;;
//}

// PARSER ********************************************************************************

class SqlParser extends Parser;
options {
    k = 2;
}

// starting rule
statement
    : selectStatement (SEMICOLON)? EOF
    ;

selectStatement
    :
    queryExpression
    (computeClause)?
    (forClause)?
    (optionClause)?
    ;

queryExpression
    : subQueryExpression (unionOperator subQueryExpression)* (orderByClause)?
    ;

subQueryExpression
    :
      querySpecification
    | LPAREN queryExpression RPAREN
    ;

querySpecification
    :
    selectClause
    (fromClause)?
    (whereClause)?
    (groupByClause (havingClause)? )?
    ;
        
selectClause
    : SELECT (ALL | DISTINCT)? (TOP Integer (PERCENT)? (WITH TIES)? )? selectList
    ;

whereClause
    : WHERE searchCondition
    ;

orderByClause
    : ORDER BY expression (ASC | DESC)? (COMMA expression (ASC | DESC)? )*
    ;

groupByClause
    : GROUP BY (ALL)? expression (COMMA expression)* (WITH (CUBE | ROLLUP) )?
    ;

havingClause
    : HAVING searchCondition
    ;

optionClause
    : OPTION LPAREN queryHint (COMMA queryHint)* RPAREN
    ;

queryHint
    :
      (HASH | ORDER) GROUP
    | (CONCAT | HASH | MERGE) UNION
    | (LOOP | MERGE | HASH) JOIN
    | FAST Integer
    | FORCE ORDER
    | MAXDOP Integer
    | ROBUST PLAN
    | KEEP PLAN
    | KEEPFIXED PLAN
    | EXPAND VIEWS
    ;

forClause
    :
    FOR (
      BROWSE
    | XML (RAW | AUTO | EXPLICIT) (COMMA XMLDATA)? (COMMA ELEMENTS)? (COMMA BINARY BASE64)
    )
    ;

computeClause
    :
    COMPUTE
    // only allowed functions are: AVG, COUNT, MAX, MIN, STDEV, STDEVP, VAR, VARP, SUM
    identifier LPAREN expression RPAREN
    (COMMA identifier LPAREN expression RPAREN)*
    (BY expression (COMMA expression)* )?
    ;

searchCondition
    : subSearchCondition ( (AND | OR) subSearchCondition )*
    ;

subSearchCondition
    :
    (NOT)? (
          (LPAREN searchCondition RPAREN) =&gt; LPAREN searchCondition RPAREN
        | predicate
        )
    ;

predicate
    :
    (
      expression (
        // expression comparisonOperator expression
        comparisonOperator (
            expression
          | (ALL | SOME | ANY) LPAREN selectStatement RPAREN
          )
      | IS (NOT)? NULL
      | (NOT)? (
            LIKE expression (ESCAPE expression)? // only single char
          | BETWEEN expression AND expression
          | IN LPAREN (
                (selectStatement) =&gt; selectStatement
              | expression (COMMA expression)*
              ) RPAREN
          )
      | CONTAINS LPAREN (dbObject | STAR) COMMA (stringLiteral | Variable) RPAREN
      | FREETEXT LPAREN (dbObject | STAR) COMMA (stringLiteral | Variable) RPAREN
      )
    | EXISTS LPAREN selectStatement RPAREN
    )
    ;

selectList
    : selectItem ( COMMA selectItem )*
    ;

selectItem
    :
      STAR // &quot;*, *&quot; is a valid select list
    | (
      // starts with: &quot;alias = column_name&quot;
      (alias2) =&gt; (
          (alias2 dbObject COMMA) =&gt; alias2 column
        | (alias2 dbObject (binaryOperator | LPAREN)) =&gt; alias2 expression
        | (alias2 column) =&gt; alias2 column
        | (alias2 expression) =&gt; alias2 expression
        )

      // all table columns: &quot;table.*&quot;
    | (tableColumns) =&gt; tableColumns
    
      // some shortcuts:
    | (dbObject (alias1)? COMMA) =&gt; column (alias1)?
    | (dbObject (binaryOperator | LPAREN) ) =&gt; expression (alias1)?
    
      // less obvious cases:
    | (column) =&gt; column (alias1)?
    | (expression) =&gt; expression (alias1)?
    )
    ;

fromClause
    : FROM tableSource (COMMA tableSource)*
    ;

tableSource
    : subTableSource (joinedTable)*
    ;

subTableSource
    :                                                           
	{String s=null;}
    (
      LPAREN (
            (joinedTables) =&gt; joinedTables RPAREN
          | (queryExpression) =&gt; queryExpression RPAREN alias1 // &quot;derived table&quot;, mandatory alias
          )
    | (function) =&gt; function (alias1)?
    | s=dbObject
	{System.out.println(s);}
                (alias1)? ( (WITH)? LPAREN tableHint (COMMA tableHint)*
RPAREN )?
 | Variable (alias1)?
    | (CONTAINSTABLE | FREETEXTTABLE) LPAREN
        dbObject COMMA (dbObject | STAR) COMMA (stringLiteral | Variable)
(COMMA Integer)?
        RPAREN (alias1)?
    | COLON COLON function (alias1)? // built-in function
    )
    ;

joinedTable
    :
      CROSS JOIN subTableSource
      // &quot;joinHint JOIN&quot; is invalid join expression
    | ( (INNER | (LEFT | RIGHT | FULL) (OUTER)? ) (joinHint)? )? JOIN tableSource ON searchCondition
    ;    

joinedTables
    : subTableSource (joinedTable)+
    ;

joinHint
    :
      LOOP
    | HASH
    | MERGE
    | REMOTE
    ;

tableHint
    :
      INDEX (
          LPAREN (identifier | Integer) ( COMMA (identifier | Integer) )* RPAREN
        | ASSIGNEQUAL identifier    // old index hint syntax
        )
    | FASTFIRSTROW
    | HOLDLOCK
    | NOLOCK
    | PAGLOCK
    | READCOMMITED
    | READPAST
    | READUNCOMMITED
    | REPEATABLEREAD
    | ROWLOCK
    | SERIALIZABLE
    | TABLOCK
    | TABLOCKX
    | UPDLOCK
    | XLOCK
    ;

collate
    : COLLATE identifier
    ;
    
alias1
    : // alias name can also be single-quoted literal (but not for table names)
    (AS)? (
          identifier
        | stringLiteral
        | keywordAsIdentifier
        )
    ;

alias2
    :
    (
      identifier
    | stringLiteral
    | keywordAsIdentifier
    )
    ASSIGNEQUAL
    ;
    
tableColumns
    :
    o:dbObject DOT_STAR
    ;

column
    :
    (PLUS)* // &quot;++column_name&quot; is valid and updatable column name
    (
      dbObject
      // for expression like &quot;(column)&quot; SQL Server returns updatable column
    | LPAREN column RPAREN
    )
    (collate)? // it is not well documented but COLLATE can be used almost anywhere ...
    ;

expression
    : // current definition ignores operator precedence
      subExpression (binaryOperator subExpression)*
    ;

subExpression
    :
    (unaryOperator)?
    (
      constant
    | Variable
    | (function) =&gt; function
    | LPAREN (
          (selectStatement) =&gt; selectStatement // select statement returning a single value
        | expression
        ) RPAREN
    | dbObject    // column
    | parameterlessFunction
    | caseFunction
    | castFunction
    )
    (collate)?    // it is not well documented but COLLATE can be used almost everywhere ...
    ;

// todo: create a separate rule for aggregate functions
function
    : // LEFT and RIGHT keywords are also function names
    (dbObject | LEFT | RIGHT) LPAREN (
          expression (COMMA expression)*
        | STAR    // aggregate functions like Count(), Checksum() accept &quot;*&quot; as a parameter
        | (ALL | DISTINCT) (STAR | expression) // aggregate function
        | Variable ASSIGNEQUAL expression (COMMA Variable ASSIGNEQUAL expression)*
        )?
    RPAREN
    ;

caseFunction
    : CASE (
          expression (WHEN expression THEN expression)+
        | (WHEN searchCondition THEN expression)+    // boolean expression
        )
    (ELSE expression)? END
    ;

castFunction
    : CAST LPAREN expression AS identifier (LPAREN Integer (COMMA Integer)? RPAREN)? RPAREN
    ;

dbObject returns [String s]
    // server.catalog.schema.object
    // server.catalog..object
    :
    (s=identifier | IDENTITYCOL | ROWGUIDCOL | keywordAsIdentifier) (
          DOT (identifier | IDENTITYCOL | ROWGUIDCOL | keywordAsIdentifier)
        | (DOT DOT) =&gt; DOT DOT (identifier | IDENTITYCOL | ROWGUIDCOL | keywordAsIdentifier)
    )*
    ;

parameterlessFunction
    : // any others ?
      CURRENT_TIMESTAMP
    | CURRENT_USER
    | SESSION_USER
    | SYSTEM_USER
    ;

systemVariable
    :
      F_CONNECTIONS 
    | F_CPU_BUSY 
    | F_CURSOR_ROWS 
    | F_DATEFIRST 
    | F_DBTS 
    | F_ERROR 
    | F_FETCH_STATUS 
    | F_IDENTITY 
    | F_IDLE 
    | F_IO_BUSY 
    | F_LANGID 
    | F_LANGUAGE 
    | F_LOCK_TIMEOUT 
    | F_MAX_CONNECTIONS 
    | F_MAX_PRECISION 
    | F_NESTLEVEL 
    | F_OPTIONS 
    | F_PACK_RECEIVED 
    | F_PACK_SENT 
    | F_PACKET_ERRORS 
    | F_PROCID 
    | F_REMSERVER 
    | F_ROWCOUNT 
    | F_SERVERNAME 
    | F_SERVICENAME 
    | F_SPID 
    | F_TEXTSIZE 
    | F_TIMETICKS 
    | F_TOTAL_ERRORS 
    | F_TOTAL_READ 
    | F_TOTAL_WRITE 
    | F_TRANCOUNT 
    | F_VERSION
    ;    

keywordAsIdentifier
    :
    (
      AUTO
    | BASE64
    | BINARY
    | CAST
    | CONCAT
    | CUBE
    | ELEMENTS
    | EXPAND
    | EXPLICIT
    | FAST
    | FASTFIRSTROW
    | FORCE
    | HASH
    | KEEP
    | KEEPFIXED
    | LOOP
    | MAXDOP
    | MERGE
    | NOLOCK
    | PAGLOCK
    | RAW
    | READCOMMITED
    | READPAST
    | READUNCOMMITED
    | REMOTE
    | REPEATABLEREAD
    | ROBUST
    | ROLLUP
    | ROWLOCK
    | SERIALIZABLE
    | TABLOCK
    | TABLOCKX
    | TIES
    | UPDLOCK
    | VIEWS
    | XLOCK
    | XML
    | XMLDATA
    )
    ;

stringLiteral
    :
      UnicodeStringLiteral
    | ASCIIStringLiteral
    ;

identifier returns [String s]
    :
      a:NonQuotedIdentifier			{s=a.getText();}
    | b:QuotedIdentifier			{s=b.getText();}
    ;

constant
    : Integer | Real | NULL | stringLiteral | HexLiteral | Currency | ODBCDateTime | systemVariable
    ;

unaryOperator
    : PLUS | MINUS | TILDE
    ;
    
binaryOperator
    : arithmeticOperator | bitwiseOperator
    ;
    
arithmeticOperator
    : PLUS | MINUS | STAR | DIVIDE | MOD
    ;

bitwiseOperator
    : AMPERSAND | TILDE | BITWISEOR | BITWISEXOR
    ;

comparisonOperator
    :
      ASSIGNEQUAL | NOTEQUAL1 | NOTEQUAL2 | LESSTHANOREQUALTO1 | LESSTHANOREQUALTO2 
    | LESSTHAN | GREATERTHANOREQUALTO1 | GREATERTHANOREQUALTO2 | GREATERTHAN
    ;
        
logicalOperator
    : ALL | AND | ANY | BETWEEN | EXISTS | IN | LIKE | NOT | OR | SOME
    ;

unionOperator
    : UNION (ALL)?
    ;
    
// LEXER *********************************************************************************

class SqlLexer extends Lexer;

options {
    testLiterals = false;
    k = 2;
    caseSensitive = false;
    caseSensitiveLiterals = false;
    charVocabulary='\u0000'..'\uFFFE';
}

tokens {
    ADD = &quot;add&quot; ;
    ALL = &quot;all&quot; ;
    ALTER = &quot;alter&quot; ;
    AND = &quot;and&quot; ;
    ANY = &quot;any&quot; ;
    AS = &quot;as&quot; ;
    ASC = &quot;asc&quot; ;
    AUTHORIZATION = &quot;authorization&quot; ;
    AUTO = &quot;auto&quot; ;
    BACKUP = &quot;backup&quot; ;
    BASE64 = &quot;base64&quot; ;
    BEGIN = &quot;begin&quot; ;
    BETWEEN = &quot;between&quot; ;
    BINARY = &quot;binary&quot; ;
    BREAK = &quot;break&quot; ;
    BROWSE = &quot;browse&quot; ;
    BULK = &quot;bulk&quot; ;
    BY = &quot;by&quot; ;
    CASCADE = &quot;cascade&quot; ;
    CASE = &quot;case&quot; ;
    CAST = &quot;cast&quot; ;
    CHECK = &quot;check&quot; ;
    CHECKPOINT = &quot;checkpoint&quot; ;
    CLOSE = &quot;close&quot; ;
    CLUSTERED = &quot;clustered&quot; ;
    // COALESCE = &quot;coalesce&quot; ;
    COLLATE = &quot;collate&quot; ;
    COLUMN = &quot;column&quot; ;
    COMMIT = &quot;commit&quot; ;
    COMPUTE = &quot;compute&quot; ;
    CONCAT = &quot;concat&quot; ;
    CONSTRAINT = &quot;constraint&quot; ;
    CONTAINS = &quot;contains&quot; ;
    CONTAINSTABLE = &quot;containstable&quot; ;
    CONTINUE = &quot;continue&quot; ;
    // CONVERT = &quot;convert&quot; ;
    CREATE = &quot;create&quot; ;
    CROSS = &quot;cross&quot; ;
    CUBE = &quot;cube&quot; ;
    CURRENT = &quot;current&quot; ;
    CURRENT_DATE = &quot;current_date&quot; ;
    CURRENT_TIME = &quot;current_time&quot; ;
    CURRENT_TIMESTAMP = &quot;current_timestamp&quot; ;
    CURRENT_USER = &quot;current_user&quot; ;
    CURSOR = &quot;cursor&quot; ;
    DATABASE = &quot;database&quot; ;
    DBCC = &quot;dbcc&quot; ;
    DEALLOCATE = &quot;deallocate&quot; ;
    DECLARE = &quot;declare&quot; ;
    DEFAULT = &quot;default&quot; ;
    DELETE = &quot;delete&quot; ;
    DENY = &quot;deny&quot; ;
    DESC = &quot;desc&quot; ;
    DISK = &quot;disk&quot; ;
    DISTINCT = &quot;distinct&quot; ;
    DISTRIBUTED = &quot;distributed&quot; ;
    DOUBLE = &quot;double&quot; ;
    DROP = &quot;drop&quot; ;
    // DUMMY = &quot;dummy&quot; ;
    DUMP = &quot;dump&quot; ;
    ELEMENTS = &quot;elements&quot; ;
    ELSE = &quot;else&quot; ;
    END = &quot;end&quot; ;
    ERRLVL = &quot;errlvl&quot; ;
    ESCAPE = &quot;escape&quot; ;
    EXCEPT = &quot;except&quot; ;
    EXEC = &quot;exec&quot; ;
    EXECUTE = &quot;execute&quot; ;
    EXISTS = &quot;exists&quot; ;
    EXIT = &quot;exit&quot; ;
    EXPAND = &quot;expand&quot; ;
    EXPLICIT = &quot;explicit&quot; ;
    FAST = &quot;fast&quot; ;
    FASTFIRSTROW = &quot;fastfirstrow&quot; ;
    FETCH = &quot;fetch&quot; ;
    FILE = &quot;file&quot; ;
    FILLFACTOR = &quot;fillfactor&quot; ;
    FOR = &quot;for&quot; ;
    FORCE = &quot;force&quot; ;
    FOREIGN = &quot;foreign&quot; ;
    FREETEXT = &quot;freetext&quot; ;
    FREETEXTTABLE = &quot;freetexttable&quot; ;
    FROM = &quot;from&quot; ;
    FULL = &quot;full&quot; ;
    FUNCTION = &quot;function&quot; ;
    GOTO = &quot;goto&quot; ;
    GRANT = &quot;grant&quot; ;
    GROUP = &quot;group&quot; ;
    HASH = &quot;hash&quot; ;
    HAVING = &quot;having&quot; ;
    HOLDLOCK = &quot;holdlock&quot; ;
    IDENTITY = &quot;identity&quot; ;
    IDENTITY_INSERT = &quot;identity_insert&quot; ;
    IDENTITYCOL = &quot;identitycol&quot; ;
    IF = &quot;if&quot; ;
    IN = &quot;in&quot; ;
    INDEX = &quot;index&quot; ;
    INNER = &quot;inner&quot; ;
    INSERT = &quot;insert&quot; ;
    INTERSECT = &quot;intersect&quot; ;
    INTO = &quot;into&quot; ;
    IS = &quot;is&quot; ;
    JOIN = &quot;join&quot; ;
    KEEP = &quot;keep&quot; ;
    KEEPFIXED = &quot;keepfixed&quot; ;
    KEY = &quot;key&quot; ;
    KILL = &quot;kill&quot; ;
    LEFT = &quot;left&quot; ;
    LIKE = &quot;like&quot; ;
    LINENO = &quot;lineno&quot; ;
    LOAD = &quot;load&quot; ;
    LOOP = &quot;loop&quot; ;
    MAXDOP = &quot;maxdop&quot; ;
    MERGE = &quot;merge&quot; ;
    NATIONAL = &quot;national&quot; ;
    NOCHECK = &quot;nocheck&quot; ;
    NOLOCK = &quot;nolock&quot; ;
    NONCLUSTERED = &quot;nonclustered&quot; ;
    NOT = &quot;not&quot; ;
    NULL = &quot;null&quot; ;
    // NULLIF = &quot;nullif&quot; ;
    OF = &quot;of&quot; ;
    OFF = &quot;off&quot; ;
    OFFSETS = &quot;offsets&quot; ;
    ON = &quot;on&quot; ;
    OPEN = &quot;open&quot; ;
    OPENDATASOURCE = &quot;opendatasource&quot; ;
    OPENQUERY = &quot;openquery&quot; ;
    OPENROWSET = &quot;openrowset&quot; ;
    OPENXML = &quot;openxml&quot; ;
    OPTION = &quot;option&quot; ;
    OR = &quot;or&quot; ;
    ORDER = &quot;order&quot; ;
    OUTER = &quot;outer&quot; ;
    OVER = &quot;over&quot; ;
    PAGLOCK = &quot;paglock&quot; ;
    PERCENT = &quot;percent&quot; ;
    PLAN = &quot;plan&quot; ;
    PRECISION = &quot;precision&quot; ;
    PRIMARY = &quot;primary&quot; ;
    PRINT = &quot;print&quot; ;
    PROC = &quot;proc&quot; ;
    PROCEDURE = &quot;procedure&quot; ;
    PUBLIC = &quot;public&quot; ;
    RAISERROR = &quot;raiserror&quot; ;
    RAW = &quot;raw&quot; ;
    READ = &quot;read&quot; ;
    READCOMMITED = &quot;readcommited&quot; ;
    READPAST = &quot;readpast&quot; ;
    READTEXT = &quot;readtext&quot; ;
    READUNCOMMITED = &quot;readuncommited&quot; ;
    RECONFIGURE = &quot;reconfigure&quot; ;
    REFERENCES = &quot;references&quot; ;
    REMOTE = &quot;remote&quot; ;
    REPEATABLEREAD = &quot;repeatableread&quot; ;
    REPLICATION = &quot;replication&quot; ;
    RESTORE = &quot;restore&quot; ;
    RESTRICT = &quot;restrict&quot; ;
    RETURN = &quot;return&quot; ;
    REVOKE = &quot;revoke&quot; ;
    RIGHT = &quot;right&quot; ;
    ROBUST = &quot;robust&quot; ;
    ROLLBACK = &quot;rollback&quot; ;
    ROLLUP = &quot;rollup&quot; ;
    ROWCOUNT = &quot;rowcount&quot; ;
    ROWGUIDCOL = &quot;rowguidcol&quot; ;
    ROWLOCK = &quot;rowlock&quot; ;
    RULE = &quot;rule&quot; ;
    SAVE = &quot;save&quot; ;
    SCHEMA = &quot;schema&quot; ;
    SELECT = &quot;select&quot; ;
    SERIALIZABLE = &quot;serializable&quot; ;
    SESSION_USER = &quot;session_user&quot; ;
    SET = &quot;set&quot; ;
    SETUSER = &quot;setuser&quot; ;
    SHUTDOWN = &quot;shutdown&quot; ;
    SOME = &quot;some&quot; ;
    STATISTICS = &quot;statistics&quot; ;
    SYSTEM_USER = &quot;system_user&quot; ;
    TABLE = &quot;table&quot; ;
    TABLOCK = &quot;tablock&quot; ;
    TABLOCKX = &quot;tablockx&quot; ;
    TEXTSIZE = &quot;textsize&quot; ;
    THEN = &quot;then&quot; ;
    TIES = &quot;ties&quot; ;
    TO = &quot;to&quot; ;
    TOP = &quot;top&quot; ;
    TRAN = &quot;tran&quot; ;
    TRANSACTION = &quot;transaction&quot; ;
    TRIGGER = &quot;trigger&quot; ;
    TRUNCATE = &quot;truncate&quot; ;
    TSEQUAL = &quot;tsequal&quot; ;
    UNION = &quot;union&quot; ;
    UNIQUE = &quot;unique&quot; ;
    UPDATE = &quot;update&quot; ;
    UPDATETEXT = &quot;updatetext&quot; ;
    UPDLOCK = &quot;updlock&quot; ;
    USE = &quot;use&quot; ;
    USER = &quot;user&quot; ;
    VALUES = &quot;values&quot; ;
    VARYING = &quot;varying&quot; ;
    VIEW = &quot;view&quot; ;
    VIEWS = &quot;views&quot; ;
    WAITFOR = &quot;waitfor&quot; ;
    WHEN = &quot;when&quot; ;
    WHERE = &quot;where&quot; ;
    WHILE = &quot;while&quot; ;
    WITH = &quot;with&quot; ;
    WRITETEXT = &quot;writetext&quot; ;
    XLOCK = &quot;xlock&quot; ;
    XML = &quot;xml&quot; ;
    XMLDATA = &quot;xmldata&quot; ;
    
    // system variables
    F_CONNECTIONS = &quot;@@connections&quot; ;
    F_CPU_BUSY = &quot;@@cpu_busy&quot; ;
    F_CURSOR_ROWS = &quot;@@cursor_rows&quot; ;
    F_DATEFIRST = &quot;@@datefirst&quot; ;
    F_DBTS = &quot;@@dbts&quot; ;
    F_ERROR = &quot;@@error&quot; ;
    F_FETCH_STATUS = &quot;@@fetch_status&quot; ;
    F_IDENTITY = &quot;@@identity&quot; ;
    F_IDLE = &quot;@@idle&quot; ;
    F_IO_BUSY = &quot;@@io_busy&quot; ;
    F_LANGID = &quot;@@langid&quot; ;
    F_LANGUAGE = &quot;@@language&quot; ;
    F_LOCK_TIMEOUT = &quot;@@lock_timeout&quot; ;
    F_MAX_CONNECTIONS = &quot;@@max_connections&quot; ;
    F_MAX_PRECISION = &quot;@@max_precision&quot; ;
    F_NESTLEVEL = &quot;@@nestlevel&quot; ;
    F_OPTIONS = &quot;@@options&quot; ;
    F_PACK_RECEIVED = &quot;@@pack_received&quot; ;
    F_PACK_SENT = &quot;@@pack_sent&quot; ;
    F_PACKET_ERRORS = &quot;@@packet_errors&quot; ;
    F_PROCID = &quot;@@procid&quot; ;
    F_REMSERVER = &quot;@@remserver&quot; ;
    F_ROWCOUNT = &quot;@@rowcount&quot; ;
    F_SERVERNAME = &quot;@@servername&quot; ;
    F_SERVICENAME = &quot;@@servicename&quot; ;
    F_SPID = &quot;@@spid&quot; ;
    F_TEXTSIZE = &quot;@@textsize&quot; ;
    F_TIMETICKS = &quot;@@timeticks&quot; ;
    F_TOTAL_ERRORS = &quot;@@total_errors&quot; ;
    F_TOTAL_READ = &quot;@@total_read&quot; ;
    F_TOTAL_WRITE = &quot;@@total_write&quot; ;
    F_TRANCOUNT = &quot;@@trancount&quot; ;
    F_VERSION = &quot;@@version&quot; ;
}

// Operators

protected DOT:; // generated as a part of Number rule
COLON : ':' ;
COMMA : ',' ;
SEMICOLON : ';' ;

LPAREN : '(' ;
RPAREN : ')' ;
//LSQUARE : '[' ;
//RSQUARE : ']' ;

ASSIGNEQUAL : '=' ;
NOTEQUAL1 : &quot;&lt;&gt;&quot; ;
NOTEQUAL2 : &quot;!=&quot; ;
LESSTHANOREQUALTO1 : &quot;&lt;=&quot; ;
LESSTHANOREQUALTO2 : &quot;!&gt;&quot; ;
LESSTHAN : &quot;&lt;&quot; ;
GREATERTHANOREQUALTO1 : &quot;&gt;=&quot; ;
GREATERTHANOREQUALTO2 : &quot;!&lt;&quot; ;
GREATERTHAN : &quot;&gt;&quot; ;

DIVIDE : '/' ;
PLUS : '+' ;
MINUS : '-' ;
STAR : '*' ;
MOD : '%' ;

AMPERSAND : '&amp;' ;
TILDE : '~' ;
BITWISEOR : '|' ;
BITWISEXOR : '^' ;
DOT_STAR : &quot;.*&quot; ;

Whitespace
    : (' ' | '\t' | '\n' | '\r')
    { _ttype = Token.SKIP; }
    ;

// COMMENTS
SingleLineComment
    : &quot;--&quot;( ~('\r' | '\n') )*
    { _ttype = Token.SKIP; }
    ;

MultiLineComment
    : &quot;/*&quot; (~'*')* '*' ('*' | ( ~('*' | '/') (~'*')* '*') )* '/'
    { _ttype = Token.SKIP; }
    ;

// LITERALS

protected
Letter
    : 'a'..'z' | '_' | '#' | '@' | '\u0080'..'\ufffe'
    ;

protected
Digit
    : '0'..'9'
    ;

protected
Integer :;

protected
Real :;

protected
Exponent
    : 'e' ( '+' | '-' )? (Digit)+
    ;

Number
    :
      ( (Digit)+ ('.' | 'e') ) =&gt; (Digit)+ ( '.' (Digit)* (Exponent)? | Exponent) { _ttype = Real; }
    | '.' { _ttype = DOT; } ( (Digit)+ (Exponent)? { _ttype = Real; } )?
    | (Digit)+ { _ttype = Integer; }
    | &quot;0x&quot; ('a'..'f' | Digit)* { _ttype = HexLiteral; } // &quot;0x&quot; is valid hex literal
    ;

protected
Currency
    : // generated as a part of NonQuotedIdentifier rule
    ('$' | '\u00a3'..'\u00a5' | '\u09f2'..'\u09f3' | '\u0e3f' | '\u20a0'..'\u20a4' | '\u20a6'..'\u20ab')
    ((Digit)+ ('.' (Digit)* )? | '.' (Digit)+)
    ;

ODBCDateTime
    : '{' (Whitespace)? (&quot;ts&quot; | 't' | 'd') (Whitespace)?
        ('n')? '\'' (~'\'')* '\'' ( '\'' (~'\'')* '\'' )* (Whitespace)? '}'
    ;

NonQuotedIdentifier
    options { testLiterals = true; }
    :
      (Currency) =&gt; Currency { _ttype = Currency; }
    | ('a'..'z' | '_' | '#' | '\u0080'..'\ufffe') (Letter | Digit)* // first char other than '@'
    ;

QuotedIdentifier
    :
    (
      '[' (~']')* ']' (']' (~']')* ']')*
    | '&quot;' (~'&quot;')* '&quot;' ('&quot;' (~'&quot;')* '&quot;')*
    )
    ;
    
Variable
    // test for literals in case of a function begining with '@' (eg. &quot;@@ERROR&quot;)
    options { testLiterals = true; }
    : '@' (Letter | Digit)+
    ;
    
ASCIIStringLiteral
    :
    '\'' (~'\'')* '\'' ( '\'' (~'\'')* '\'' )*
    ;

UnicodeStringLiteral
    :
    'n' '\'' (~'\'')* '\'' ( '\'' (~'\'')* '\'' )*
    ;

// Numeric Constants

protected
HexLiteral // generated as a part of Number rule
    : // &quot;0x&quot; ('0'..'9' | 'a'..'f')*
    ;
</PRE>



<!--endarticle-->
    <HR>
    <P><UL>
        <!--threads-->
	<LI>Previous message: <A HREF="012147.html">[antlr-interest] how to print tokens?
</A></li>
	<LI>Next message: <A HREF="012154.html">[antlr-interest] how to print tokens?
</A></li>
         <LI> <B>Messages sorted by:</B> 
              <a href="date.html#12153">[ date ]</a>
              <a href="thread.html#12153">[ thread ]</a>
              <a href="subject.html#12153">[ subject ]</a>
              <a href="author.html#12153">[ author ]</a>
         </LI>
       </UL>

<hr>
<a href="http://www.antlr.org/mailman/listinfo/antlr-interest">More information about the antlr-interest
mailing list</a><br>
</body></html>
