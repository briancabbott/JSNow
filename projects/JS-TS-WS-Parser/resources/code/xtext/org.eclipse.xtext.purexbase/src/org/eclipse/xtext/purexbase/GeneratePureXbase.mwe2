module org.eclipse.xtext.purexbase.PureXbase

import org.eclipse.emf.mwe.utils.*
import org.eclipse.xtext.generator.*

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*

var rootPath = ".."

var fileHeader = "/*******************************************************************************
 * Copyright (c) 2011, \${year} itemis AG (http://www.itemis.eu) and others.
 * This program and the accompanying materials are made available under the
 * terms of the Eclipse Public License 2.0 which is available at
 * http://www.eclipse.org/legal/epl-2.0.
 *
 * SPDX-License-Identifier: EPL-2.0
 *******************************************************************************/"

Workflow {
	bean = StandaloneSetup {
		platformUri = rootPath
	}
	
	component = XtextGenerator {
		configuration = {
			project = StandardProjectConfig {
				baseName = "org.eclipse.xtext.purexbase"
				rootPath = rootPath
				runtimeTest = {
					enabled = true
				}
				eclipsePlugin = {
					enabled = true
					root = "../org.eclipse.xtext.purexbase.ui"
				}
				eclipsePluginTest = {
					enabled = true
					root = "../org.eclipse.xtext.purexbase.eclipse.tests"
				}
				createEclipseMetaData = true
			}
			code = {
				encoding = "UTF-8"
				lineDelimiter = "\n"
				fileHeader = fileHeader
				preferXtendStubs = false
			}
		}
		language = StandardLanguage {
			name = "org.eclipse.xtext.purexbase.PureXbase"
			fileExtensions = "xbase"
    		referencedResource = "platform:/resource/org.eclipse.xtext.common.types/model/JavaVMTypes.genmodel"
    		referencedResource = "platform:/resource/org.eclipse.xtext.xbase/model/Xtype.ecore"
    		referencedResource = "platform:/resource/org.eclipse.xtext.xbase/model/Xbase.genmodel"
    		referencedResource = "platform:/resource/org.eclipse.xtext.xbase/src/org/eclipse/xtext/xbase/Xtype.xtext"
    		referencedResource = "platform:/resource/org.eclipse.xtext.xbase/src/org/eclipse/xtext/xbase/Xbase.xtext"

			serializer = {
				generateStub = false
			}
			validator = {
				// composedCheck = "org.eclipse.xtext.validation.NamesAreUniqueValidator"
			}
			formatter = {
				generateStub = true
				generateXtendStub = false
			}
			junitSupport = {
				generateXtendStub = false
			}
		}
	}
}


